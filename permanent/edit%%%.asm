 EDIT***:,NAME,.31.12.80
 *REGIM//:,LC,1
 *TIKTAK* :,LC,25.
 MONCARD* :,LC,25.
 MONREAD* :,SUBP,.
 ERROFLG*:,LC,1
 CREAD:,EQU,CAREAD
 NOLISFL*:,LC,1
 ID1:,EQU,IDID.
 ID2:,EQU,IDID+1.
 FORMKOZA:,SUBP,
 RM*:,LC,5
C      П/П ДЛЯ PAБOTЫ C
C      ЛEHTAMИ CИMBOЛOB
 SIMFAIL*:,SUBP,
 SIMFAIL*:I,BLOCK,DO*LENT,P1,M1,W*P1,
 ,CONT,R*P1,W*M1,R*M1,P1*W,
 ,CONT,P1*R,M1*W,M1*R,
 ,CONT,L1*TO*L2,RSIM,WSIM,
 ,CONT,L2*TO*L1
C------YПPABЛEHИE ЗAГPYЗKOЙ
 CHKBEG*:,SUBP,
 PRINT8//:,SUBP,
 TABSIMIS:,SUBP,
 SIM*:,EQU,52B .по таблице в TABSIMIS
 SIM/:,EQU,57B .по таблице в TABSIMIS
 *MONCRD*:,LC,12
 PRINT8:,SUBP,
 READ*:,SUBP,
 REWORD:,SUBP,
 WRWORD:,SUBP,
 PARAMT*:,SUBP,
 RECARD:,SUBP,
 OUT*OUT:,LC,1
 REWORD/:,PC,1024
 WRWORD/:,PC,1024
 BUFFER:,PC,1010
 XREAD/:,LC,2
 XWRITE/:,LC,2
 NAME*:,LC,2
 MIST10*:LC,BLOCK,MIS,CODE,NUMB
 MONCARD/:,LC,14
 STARSTA:,LC,1
 BIGBUF:,PC,811
 COSYTISO:,SUBP,
 OCTTDEC:,SUBP,.
 BASE:,BSS,
 14,VTM,-12
 BASE1:14,ITS,13.MOD 1-13
 14,VLM,BASE1
 7,BASE ,BASE
C      HAЧAЛЬHOE BOCCTAHOBЛEHИE
 Q:,EQU,MYMY-BLYPR.N-1
 MQ:,EQU,BLYPR-MYMY.-(N-1)
 14,VTM,MQ
 ,XTS,
 G* :14,ATX,BLYPR+Q
 14,VLM,G*
 ,XTA,=H
 ,ATX,NUMBERI
 ,ATX,NETAL
 ,ATX,NETAL+1
 ,XTA,NUMBERE
 ,ATX,NUMBERI+1
 ,XTA,20B
 ,ATX,SJUMP
C      CTAHДAPTHAЯ PEAKЦИЯ HA ПPEPЫBAHИЯ
 ,XTA,MISTIKA
 ,CTX,20B
 ,XTA,*REGIM//
 ,ATX,ZAKAZP
C      AДPEC OБPAБOTKИ NONTEXT CARD
 14,VTM,NOTXR
 ,ITA,14
 ,AOX,*REGIM//
 ,ATX,*REGIM//
 14,VTM,3
 ,ITA,14
 ,*50,102B.3 ПPE-Я
 ,ITA,15
 ,ATX,MAG
 14,VTM,52B.*
 ,ITA,14
 ,ATX,FOR*
 ,XTA,=1
 ,UTC,OUT*OUT
 ,ATX,
 10,VTM,
 ,CALL,PARAMT*.ПOДBOД
 11,VZM,BASI
 10,VTM,3.IDENT
 ,CALL,PARAMT*
 13,VJM,XOXO.PARAM1
 11,VZM,BASI
 10,VTM,3.IDENT
 ,CALL,PARAMT*
 13,VJM,XOXO.PARAM2
 BASI:,BSS,
 13,VJM,EDNAM
C      ''ЗAKAЗЫ'' ЛEHT SIMFAIL* ДЛЯ PEД-Я ПO/
 8,VTM,SX1/
 9,VTM,CARDR
 10,VTM,12
 13,VJM,DO*LENT.ДЛЯ KAPTЫ ИЗ READ
 8,VTM,SXR/
 9,VTM,SCARD
 10,VTM,12
 13,VJM,DO*LENT.ДЛЯ PEЗYЛЬTATA PEДAKЦИИ
 ,UJ,SLCARDN
C-----      AHAЛИЗ OЧEPEДHOЙ KAPTЫ ЗAДAHИЯ
 SLCARD:13,VJM,PRINTCD.ПEЧATЬ ПPEДШECTBYЮЩEЙ
 SLCARDN:,BSS,.ПPOBEPKA KAPTЫ
C                          .И EE ЧTEHИE
 ,XTA,MONCARD*.
 ,ASN,64+40
 ,UZA,ECTЬ*BIN.HE TEKCTOBAЯ !
 ,XTA,MONCARD*.
 ,AEX,=6H*END F.
 ,UIA,ECTЬ*ISO.
 14,VTM,TOTCEE.
 ,ITS,14.
 14,UTM,3
 ,ITS,14.
 14,VTM,2
 ,ITS,14.
 ,CALL,PRINT8//.
 ,UJ,*EE FIN.HA OБP. *EE
 ECTЬ*BIN:,XTA,MONCARD*.
 ,ASN,64+40.
 ,ATX,MYMY.ПEЧATЬ HE HYЖHA
 14,VTM,13.
 ,UIA,MISTAKE.
 13,VTM,ECTЬ*BIN.
 ,UJ,MONREAD*.ПPOПYCK BIN
 ECTЬ*ISO:,BSS,.TEKCTOBAЯ KAPTA
         ,XTS,ACARD
 ,CALL,READ*.ЧTEHИE KAPTЫ
 14,VTM,-13
 MY:14,XTA,CARD+13.ПEPEПИCЬ B MONCARD/
 14,ATX,MONCARD/+13
 14,VLM,MY
C      BOCCTAH.TABSIMIS(*,/)
 14,VTM,TABSIMIS
 14,XTA,SIM*
 ,AOX,=:1.OГP.
 14,ATX,SIM*
 14,XTA,SIM/
 ,AOX,=:1.OГP.
 14,ATX,SIM/
 ,XTA,
 ,ATX,NNN.K-BO ЦИФP B YПP. CЛOBE
 8,VTM,XCARD
 9,VTM,CARD
 10,VTM,12
 13,VJM,DO*LENT.ЛEHTA CИMBOЛOB
 13,VJM,RSIM
C      AHAЛИЗ YПP.CИMBOЛA
 ,ITA,14
 ,AEX,FOR*
 ,U1A,NOYPR.NOT
 ,XTA,CARD
 ,ASN,64-8
 ,AEX,=6H(((  '000'
 ,UZA,*3/1
 ,XTA,BLYPR
 ,U1A,NOYPR
C      OПPEДEЛEHИE KAPT CTAPOГO PEДAKTOPA
 9,VTM,
 10,VTM,
 OLD:10,XTA,OLDED
 ,UZA,OLDN.HE'CTAPAЯ'KAPTA
 ,AEX,CARD
 ,UIA,OLDA.HECOBПAДEHИE ПEPBЫX CЛOB
 10,XTA,OLDED+1
 ,AEX,CARD+1
 ,UIA,OLDA.HECOBПAДEHИE BTOPЫX
 10,XTA,OLDED+2
 ,AEX,CARD+2
 ,UZA,OLDB.COBПAДEHИE TPETЬИX
 OLDA:10,UTM,3
 9,UTM,1
 ,UJ,OLD
 OLDB:,XTA,
 ,ATX,N2
 ,XTA,CARD+3
 ,AEX,=H
 ,UIA,OLDC
 ,XTA,CARD+4
 ,AEX,=H
 ,UZA,OLDN
C      OФOPMЛEHИE N1
 OLDC:,XTA,CARD+3
 ,ATX,N1
 ,ATX,NETAL
 ,XTA,CARD+4
 ,AAX,=:777774
 ,AEX,=6H''''   '12'
 ,ATX,N1+1
 ,ATX,NETAL+1
 ,XTA,CARD+5
 ,AEX,=H
 ,UIA,OLDD.ECTЬ N2
 ,XTA,CARD+6
 ,AEX,=H
 9,UZA,OLDPER.HET N2
C      OФOPMЛEHИE N2
 OLDD:,XTA,CARD+5
 ,ATX,N2
 ,XTA,CARD+6
 ,AAX,=:777774
 ,AEX,=6H''''   '12'
 ,ATX,N2+1
 9,UJ,OLDPER
C      ПEPEKЛЮЧATEЛЬ ДЛЯ 'CTAPЫX' KAPT(CM.OLDED)
 OLDPER:,UJ,*I+1.0
 :,UJ,*I+1.1
 :,UJ,*D+1.2
 :,UJ,*T+1.3
 :,UJ,*T+1.4
C      AHAЛИЗ YПP.KAPTЫ(HOBЫИ ФOPMAT)
 OLDN:,BSS,
 SL0:13,VJM,P1*R.ЧTEHИE CИMBOЛA
 11,V1M,SLA..HET OГPAHИЧИTEЛЯ
 ,UTC,TABSIMIS
 14,XTA,
 ,AAX,=:7
 ,AEX,=:1
 ,U1A,SL0.HE OГPAHИЧИTEЛЬ
 14,MTJ,13
 13,UTM,-56B
 13,VZM,SLB.TOЧKA
 13,UTM,-1
 13,VZM,SLC./
 14,MTJ,13
 13,UTM,-72B
 13,VZM,SLA
 ,UJ,SLMIST.ПЛOXOИ OГPAHИЧИTEЛЬ
 SLA:6,VTM,TWOT
 13,VJM,DOKR/.OKOHЧAHИE PEД-Я /
 ,UJ,SL1
 SLB:6,VTM,ONET
 13,VJM,DOKR/.OKOHЧAHИE PEД-Я /
 ,UJ,SL1
 SLC:6,VTM,ONE/
C      AHAЛИЗ YПPABЛЯЮЩИX CЛOB
 SL1:,BSS,
 8,VTM,XCARD
 9,VTM,CARD
 10,VTM,12
 13,VJM,DO*LENT.ЛEHTA CИMBOЛOB
 5,VTM,.N CTPOKИ TAБЛИЦЫ
 SL1A:13,VJM,P1*R
 11,V1M,SLMIST.ПЛOXOE YПP.CЛOBO
 14,MTJ,13
 13,UTM,-40B
 13,VZM,SL1A.ПPOБEЛ
 SL1B:6,UTC,
 5,XTA,
 ,UZA,SLMIST.KOHEЦ TAБЛИЦЫ
 ,ASN,64-7
 ,YTA,
 ,ITS,14
 15,AEX,
 ,UZA,SL1C.CИMBOЛЫ COBПAЛИ
 5,UTM,1
 ,UJ,SL1B
 SL1C:6,UTC,
 5,XTA,
 ,ATX,XXX.AДPEC ПP. OБPAБOTKИ
 15,ATX,
 ,AAX,=4000 0000
 ,STX,PR111.ПPИЗHAK БЛ. N YПP.KAPT
 ,AAX,=1 0000 0000
 ,U1A,SL1D.HA AHAЛИЗ ДOП.TAБЛИЦЫ
 SL1C0:13,VJM,P1*R
 11,V1M,SL1CF
 14,MTJ,13
 13,UTM,-40B
 13,VZM,SL1C0.ПPOБEЛ
 ,UTC,TABSIMIS
 14,XTA,
 ,AAX,=:7
 ,U1A,SL1C01.HE БYKBA
 ,XTA,NNN.ЦИФP EЩE HE БЫЛO
 ,UZA,SL1C0
 ,UJ,SLMIST
 SL1C01:,AEX,=:1
 ,UZA,SL1CF.OГPAHИЧИTEЛЬ
 ,XTA,PR111
 ,UZA,SLMIST.ЦИФPЫ ЗAПPEЩEHЫ
 ,XTA,NNN
 ,ASN,64-8
 ,ITS,14
 15,AEX,
 ,ATX,NNN.ЧИCЛO CЛ.HEYПP.KAPT
 ,UJ,SL1C0
 SL1CF:,XTA,XXX
 ,UZA,SLMIST.HEKOPP.YПP.CЛOBO
C      KOPP. TABSIMIS(*/ БYKBЫ)
 14,VTM,TABSIMIS
 14,XTA,SIM*
 ,AAX,=0777 7777 7777 7777
 14,ATX,SIM*.*
 14,XTA,SIM/
 ,AAX,=0777 7777 7777 7777
 14,ATX,SIM/./
 ,WTC,XXX.HA OБPAБOTKY
 ,UJ,
C      ПOИCK BTOPOГO YПP.CЛOBA
 SL1D:6,UTC,
 5,WTC,
 6,VTM,.AДPEC ДOП.TAБЛИЦЫ
 5,VTM,
 SL1D1:6,UTC,
 5,XTA,
 ,AAX,=:7777 7777
 ,UZA,SL1D2.KOHEЦ TAБЛИЦЫ
 5,UTM,1
 ,UJ,SL1D1
 SL1D2:6,UTC,
 5,XTA,
 ,ATX,XXX.AДPEC OБPAБOTKИ
 ,AAX,=4000 0000
 ,ATX,PR111.ПPИЗHAK 'N KAPT HEYПP.'
 SL1D3:5,VTM,
 SL1D4:13,VJM,P1*R
 11,V1M,SL1CF..KOHEЦ AHAЛИЗA
 14,MTJ,13
 13,UTM,-40B
 13,VZM,SL1D4.ПPOБEЛ
 ,UTC,TABSIMIS
 14,XTA,
 ,AAX,=:7
 ,UZA,SL1D5.БYKBA
 ,UJ,SL1C0+1.HE БYKBA
 SL1D5:6,UTC,
 5,XTA,
 ,AAX,=:7777 7777
 ,UZA,SL1D3.KOHEЦ TAБЛИЦЫ
 6,UTC,
 5,XTA,
 ,ASN,64-7
 ,YTA,
 ,ITS,14
 15,AEX,
 ,UZA,SL1C.ECTЬ 2-E CЛOBO
 5,UTM,1
 ,UJ,SL1D5
C------OБPAБOTKA *R
 *R:,BSS,
 13,VJM,GYKAZ.YK MБ/MЛ
 ,XTA,=1
 ,ATX,YRE.ПP. 'ECTЬ *R'
 13,VTM,*RE
C      CПPABKИ ПPEДЫДYЩEГO *R И ДAHHOГO
 FILF:,BSS,
 ,XTA,FILE
 ,ATX,FILE+1.ИMЯ ПPEД.
 ,XTA,ID1
 ,ATX,FILE.ИMЯ HACT.
 ,XTA,ZT
 ,ATX,ZT+1.'TP/ЗOH' ПPEД.
 ,XTA,ID2+2
 ,ATX,ZT.'TP/ЗOH' HACT.
 13,UJ,
 *RE:,BSS,
 13,VJM,DOKTAKE.OKOHЧAHИE TAKE
 13,VJM,DOKREAD.OK-E ПPEД.*R
 14,VTM,RRG.YK MБ/MЛ
 ,ITS,14
 ,CALL,RBEGIN.ПOДГOTOBKA K ЧTEHИЮ
 ,XTA,=:1
 ,ATX,NOENDR.ПP.'HEOKOHЧ.*R'
 *REO:,BSS,
 ,XTA,RRG
 ,ATX,AREAD.YK TEK *R
C      ЧИCTKA ПPИЗHAKOB
 ,XTA,
 ,ATX,YRE
 ,ATX,YESTAKE
 ,ATX,YESCARD
 ,ATX,BLTAKE
 ,ATX,TOUT
 ,XTA,=H
 ,ATX,NETAL.ЭTAЛOHHЫЙ N
 ,ATX,NETAL+1
 ,UJ,SLCARD
C------OБPAБOTKA *RO
 *RO:,XTA,
 ,ATX,RRG
 ,XTA,=1
 ,ATX,YRE
 13,VJM,FILF.CПPABKA
 13,VJM,DOKTAKE.OKOHЧAHИE TAKE
 13,VTM,*REO
 ,UJ,DOKREAD.OKOHЧAHИE *R
C------OБPAБOTKA *W
 *W:,BSS,
 13,VJM,GYKAZ.YK MЛ/MБ
 13,VJM,FILF/.CBOДKA.OKOHЧAHИE:
 13,VJM,DOKTAKE.TAKE
 13,VJM,DOKWRITE.*R
 14,VTM,RRG
 ,ITS,14
 ,CALL,WBEGIN.ПOДГOTOBKA ЗAПИCИ
 ,XTA,RABOTA1
 13,VJM,ENDLI.OKOHЧAHИE:
 ,XTA,RABOTA1.LIST
 13,VJM,ENDPU.PUCH
 ,XTA,RABOTA1
 13,VJM,ENDUZ.USER
 ,XTA,
 ,ATX,RABOTA1
 ,ATX,RABOTA2
 ,XTA,RRG
 ,ATX,AWRITE.YK TEK. *W
 ,XTA,=2000
 ,ATX,AOGR.ДЛИHA MЛ
 ,UTC,CODE
 ,XTA,
 ,UZA,SLCARD.HET 2-ГO ПARAM.
 10,VTM,5
 9,VTM,AOGR
 ,CALL,PARAMT*.ЧTEHИE 2-ГO
 14,V1M,SLMIST.C OШИБKOЙ
 ,UZA,SLMIST.=0
 ,UJ,*NO
C------OБPAБOTKA *WO
 *WO:,XTA,
 ,ATX,RRG
 ,UJ,*W+1
C---   CПPABKA TEK. И HACT. *W
 FILF/:,XTA,FILE/
 ,ATX,FILE/+1
 ,XTA,ID1
 ,ATX,FILE/
 ,XTA,ZT/
 ,ATX,ZT/+1
 ,XTA,ID2+2
 ,ATX,ZT/
 13,UJ,
C------OБPAБOTKA *WC
 *WC:,BSS,
 13,VJM,GYKAZ.ЧTEHИE YK
 13,VJM,FILF/.CBOДKA
C      CПACAHИE CBOДKИ
 ,XTA,FILE+1
 ,XTS,QCAR
 ,XTS,ZT+1
 ,XTS,
 ,XTA,ID1
 ,ATX,FILE+1
 ,XTA,ID2+2
 ,ATX,ZT+1
C      OKOHЧAHИE:
 13,VJM,DOKTAKE.TAKE
 13,VJM,DOKWRITE.*W
 ,XTA,RABOTA1
 13,VJM,ENDLI.LIST
 ,XTA,RABOTA1
 13,VJM,ENDPU.PUNCH
 ,XTA,RABOTA1
 13,VJM,ENDUZ.UZER
 ,XTA,
 ,ATX,RABOTA1.ШKAЛЫ
 ,ATX,RABOTA2.CEPBИCA
C      CПACAHИE X-K *R
 ,XTA,YESCARD
 ,ATX,YESCARD/
 ,XTA,NOENDR
 ,ATX,NOENDR/
 ,XTA,ZAP
 ,ATX,ZAP/
 ,XTA,=1
 ,ATX,YRE
 ,XTA,AREAD
 ,ATX,AREAD/
 ,XTA,=2000
 ,ATX,AOGR.ДЛИHA MЛ
 ,UTC,CODE
 ,XTA,
 ,UZA,*WC//.HET 2-ГO ПAPAM
 10,VTM,5
 9,VTM,AOGR
 ,CALL,PARAMT*.ЧTEHИE 2-ГO
 14,V1M,SLMIST.C OШИБKOЙ
 *WC//:,BSS,
 14,VTM,-13
 *WC1:14,XTA,CARDR+13.TEKYЩAЯ
 14,ATX,CARDR/+13.KAPTA *R
 14,VLM,*WC1
 ,XTA,RRG.YK *WC
 ,ATX,AWRITE
 ,ATX,AREAD
C      HOBЫX X-KИ *R
 ,XTA,
 ,ATX,YESCARD
 ,ATX,ZAP
 ,XTA,=1
 ,ATX,NOENDR
C      CПACAHИE X-KИ П/П RECARD
 14,VTM,XREAD/
 14,XTA,
 ,ATX,XREAD//
 14,XTA,1
 ,ATX,XREAD//+1
 12,VTM,XREAD/
 11,VTM,WRWORD/
 14,VTM,AREAD
 ,ITS,14
 ,UTC,1
 ,CALL,RBEGIN.ПOДГOTOBKA K ЧTEHИЮ
 14,VTM,AREAD
 ,ITS,14
 ,CALL,WBEGIN.K ЗAПИCИ
C      БЛOKИPOBKA ЗAПИCИ
 14,VTM,XWRITE/
 14,XTA,
 ,AOX,=1 0000 0000 0000.NO WR
 14,ATX,
 ,ATX,BL*R*W
C      ПEPEПИCЬ C ФИKT.ЗAПИCЬЮ
 13,VJM,DOKREAD
 13,VTM,XREAD/
 14,VTM,XWRITE/
 13,XTA,1
 14,AEX,1
 ,AAX,=77 7777
 ,UZA,EZON.HE ПPOЧИTAHA ЛИШHЯЯ ЗOHA
 14,XTA,1
 ,AOX,=:001
 ,ATX,RR1
 ,DRUM,RR1.ПPEДЫДYЩAЯ ЗOHA
 EZON:,BSS,
C      BOCCTAHOBЛEHИE
 *WC2:,XTA,YESCARD/
 ,ATX,YESCARD
 ,XTA,ZAP/
 ,ATX,ZAP
 ,XTA,NOENDR/
 ,ATX,NOENDR
 ,XTA,AREAD/
 ,ATX,AREAD
 14,VTM,XWRITE/
 14,XTA,
 ,AOX,=1 0000 0000 0000.NO WR
 ,AEX,=1 0000 0000 0000.NO WR
 14,ATX,
 14,VTM,-13
 *WC3:14,XTA,CARDR/+13
 14,ATX,CARDR+13
 14,VLM,*WC3
 14,VTM,XREAD/
 ,XTA,XREAD//
 14,ATX,
 ,XTA,XREAD//+1
 14,ATX,1
 ,XTA,
 ,STX,BL*R*W
 ,STX,ZT+1
 ,STX,QCAR
 ,ATX,FILE+1
 ,UJ,*NO
C      ПEPEXOД K OБЫЧHOMY ФOPMATY(R,W)
 COSY0:14,VTM,XREAD/
 14,XTA,
 ,AOX,=2000 0000 0000
 ,AEX,=2000 0000 0000
 14,ATX,
 14,VTM,XWRITE/
 14,XTA,
 ,AOX,=2000 0000 0000
 ,AEX,=2000 0000 0000
 14,ATX,
 13,UJ,
C-----      OБPAБOTKA *EE
 *EE:,BSS,
 ,XTA,CARD
 ,AEX,=6H*EXECU
 ,UZA,SLMIST
 13,VJM,DOKTAKE.OK-E TAKE
 ,XTA,=1
 ,ATX,YRE
 13,VJM,FILF.CПPABKA *R
 13,VJM,DOKREAD.*R
 13,VJM,FILF/.CПPABKA *W
C      OKOHЧAHИE:
 13,VJM,DOKWRITE.*W
 13,VJM,PRINTCD
 *EEFIN:,BSS,
 13,VJM,PRINT8//+1
 ,XTA,RABOTA1
 13,VJM,ENDLI.LIST
 ,XTA,RABOTA1
 13,VJM,ENDPU.PUNCH
 ,XTA,RABOTA1
 13,VJM,ENDUZ.UZER
 ,XTA,SJUMP
 ,CTX,20B
 ,XTA,
 ,*50,102B.1 ПPE-E
 ,XTA,ZAKAZP
 ,ATX,*REGIM//
C      BOЗBPAT ИЗ PEДAKTOPA
 ,STI,
 14,VTM,13
 14,STI,
 14,UTM,-1
 14,VIM,*-1
 ,UTC,PROGON
 ,XTA,
 13,UZA,.NONMISTAKE
 ,UTC,ZAKAZ
 ,XTA,
 ,UZA,20B.SYS. JUMP
 13,UJ,
C__________  OБPAБOTKA KAPTЫ *A...
 *A:13,VJM,DOKTAKE.
 ,XTA,=1.
 ,ATX,YRE.
 13,VJM,FILF.
 13,VJM,DOKREAD.
 13,VJM,FILF/.
 13,VTM,SLCARD.
 ,UJ,DOKWRITE.
 SLMIST:14,VTM,.HEKOPP. KAPTA
C-----      ПEЧATЬ ДИAГHOCTИKИ(I14=N)
 MISTAKE:,BSS,
 14,MTJ,1
 7,BASE,BASE
 MISTAKE1:,BSS,.BXOД I1=N
 ,WTC,MAG
 15,VTM,
 ,XTA,MYMY
 ,U1A,MYMYPR
 13,VJM,PRI.ПEЧATЬ KAPTЫ
 MYMYPR:,BSS,
 ,XTA,RABOTA1
 13,VJM,ENDPU.OK-E PUNCH
 1,MTJ,14
 12,VTM,
 14,UTM,1
 ER1:12,MTJ,11
 ER2:12,XTA,ERTEXT
 12,UTM,1
 ,U1A,ER2
 14,UTM,-1
 14,VZM,ERPRI
 ,UJ,ER1
 ERPRI:11,UTC,
 14,VTM,ERTEXT
 ,ITS,14
 12,UTC,
 14,VTM,ERTEXT-2
 ,ITS,14
 14,VTM,2
 ,ITS,14
 ,XTS,ZAKAZ
 ,AEX,=1
 ,UZA,ER3
 ,ATX,ERROFLG*
 ER3:,STX,
 ,CALL,PRINT8//
 13,VJM,MISTIKA2.OK-E *R,*W
 ,XTA,ENDE1
 ,ATX,PROGON
 ,AEX,XXX
 ,U1A,SLCARDN.NOT *EE
 ,XTA,CARD
 ,AEX,=6H*EXECU
 ,U1A,*EEFIN.NOT *EXECUTE
 ,UJ,SLCARDN.ДOKP-KA ДO *EE
 *EQRW:1,VTM,1.*R=*W
 ,UJ,MISTAKE1
 ERRBL:14,VTM,2.ПYCTOЙ OБPAЗ
 ,UJ,MISTAKE
 ERRBLT:14,VTM,3.БЫЛA *DF/
 ,UJ,MISTAKE
 NORA:1,VTM,4.HET PAБOTЫ
 ,UJ,MISTAKE1
 ERRNO/:14,VTM,5.HET OБPAЗA
 ,UJ,MISTAKE
 NAM11WC:14,VTM,6.HET KAPTЫ
 ,UJ,MISTAKE
 NAMC1:14,VTM,7.HET *R
 ,UJ,MISTAKE
 PO/MI:14,VTM,8.73 CИMBOЛA
 ,UJ,MISTAKE
 MISTIKA:,BSS,
 14,VTM,10.БЫЛИ OШИБKИ
 ,UJ,MISTAKE
 MB100:1,VTM,11.>2-X MБ
 ,UJ,MISTAKE1
 NOTXR:14,VTM,12.NONTEXT R
 ,UJ,MISTAKE
 PARMIS:14,VTM,15.OШИБ. B ПAPAMETPAX
 ,XTA,=1.
 ,ATX,MYMY.
 ,UJ,MISTAKE.KAPTA HAПEЧATAHA
C     OKOHЧAHИE *R,*W
 MISTIKA2:,BSS,
 ,ITS,13
 ,XTS,YRE
 ,UIA,MISTYPE
 13,VJM,FILF.CПPABKA *R
 MISTYPE:,BSS,
 ,XTA,
 ,ATX,NOWRIT
 ,ATX,NOENDR
 13,VJM,DOKREAD.OK-E *R
 13,VJM,FILF/.CПPABKA *W
 ,XTA,=:4
 ,ATX,MYMY
 ,AEX,CREAD
 ,UZA,BA
 13,VJM,DOKWRITE.OK-E *W
 BA:,BSS,
 ,XTA,
 ,STX,MYMY
 ,STI,13
 13,UJ,
C-----      ПOЛYЧEHИE YK MБ/MЛ(*R,*W,WC)
 GYKAZ:,ITA,13
 ,XTS,
 11,VZM,GYK.ECTЬ YK
 GYK0:,XTA,=03 0000.YK=1
 ,ATX,RR1
 ,XTA,DOKYT.ДЛЯ CBOДKИ
 ,ATX,ID2+2
 ,XTA,=6H1
 ,ATX,ID1
 ,UJ,GYKRET
 GYK:,BSS,
 9,VTM,4
 13,VJM,PARAMT.YKAЗATEЛЬ MБ/MЛ
 ,XTA,RR1
 ,ARX,=10 0000
 ,ASN,64+18
 ,U1A,SLMIST.7-OE HAПP.
 ,XTA,RR1
 ,AAX,=00 6000
 ,U1A,SLMIST.N30H>1711B.
 ,XTA,RR1
 ,AEX,=03 0000
 ,UZA,GYK0.YK=1
 ,XTA,RR1
 ,AEX,=05 0000
 ,UIA,GYKA.YK HE =2
 ,XTA,DOKYT.ДЛЯ CBOДKИ
 ,ATX,ID2+2
 ,XTA,=6H2
 ,ATX,ID1
 ,UJ,GYKRET
 GYKA:,XTA,RR1.ДЛЯ CBOДKИ
 ,ASN,64-30
 ,ATX,ID2+1
 ,UTC,*MONCRD*
 ,XTA,
 ,ATX,ID1
 GUKI:,BSS,
 ,XTA,DOKYZ
 ,ATX,ID2+2
 ,XTA,ID2+1
 ,AAX,=:4
 ,UIA,GYKRET.4-6 HAПP.
 ,XTA,ID2+1
 ,AAX,=:7
 ,AEX,=:3
 ,UZA,GYKRET.-3 HAПP.
 ,XTA,DOKYT
 ,ATX,ID2+2
 ,XTA,ID2+1
 ,AAX,=:00740
 ,UIA,SLMIST.NTP.>37B
 GYKRET:,XTA,RR1
 ,STX,RRG.RRG=YK
 ,ATI,13
 13,UJ,
 PARAMT:,BSS,.ЧTEHИE YK MЛ/MБ,ИДEHTИФ-POB
 ,ITA,13.I9-N ПP. PARAMT*
 ,XTS,MONCARD/
 ,AOX,=6H'377''0''0''0''0''0'
 ,AEX,=6H'325''0''0''0''0''0'
 ,ATX,MONCARD/
 10,VTM,
 ,CALL,PARAMT* .ПOДBOД K ПAPAMETPY
 9,MTJ,10
 9,VTM,
 ,CALL,PARAMT*.ЧTEHИE ПAPAMETPA
 14,V1M,SLMIST
 ,STX,RR1
 ,ATI,13
 13,UJ,
C---   AHAЛИЗ ПAPAMETPA *EDIT
 XOXO:,BSS,
 13,UTC,
 14,V1M,
 14,VTM,*MONCRD*
 14,XTA,
 ,ASN,64+40
 ,ATI,14
 14,UTM,-103B
 14,V1M,XA0.NON C
 ,XTA,=:1
 XA*:,BSS,
 ,ATX,ZAKAZ
 13,UJ,
 XA0:14,UTM,-2
 14,V1M,XA.NON E
 ,XTA,=1
 ,UJ,XA*
 XA:14,UTM,-7
 14,V1M,XA1
 ,XTA,=:6
 ,UJ,XO
 XA1:14,UTM,-3
 14,V1M,XA2.NON O
 ,XTA,=:4
 ,UJ,XO
 XA2:14,UTM,-5
 14,V1M,PARMIS.NON T
 ,XTA,
 XO:,XTS,*REGIM//
 ,ASN,64-3
 ,ASN,64+3
 15,AEX,
 ,ATX,*REGIM//
 13,UJ,
C-----     ЧTEHИE OЧEPEДHOЙ KAPTЫ *R
C      (ACARD-1-15 P.SYMM,)
C      (REZ:SYMM=0-*READ OLD)
 NAMCALL1:,BSS,
 ,ATX,RR1
 ,XTA,AREAD
 ,UZA,NAMC1.HET *R
 ,ITA,13
 ,XTS,RR1
 ,CALL,RECARD.ЧTEHИE KAPTЫ
 ,ATI,13
 ,XTA,QCAR
 ,ARX,=1
 ,ATX,QCAR.QCAR=QCAR+1
 ,WTC,RR1
 ,XTA,
 ,AEX,=6H*READ
 ,U1A,NC1RET1.NOT *READ
 ,WTC,RR1
 ,XTA,1
 ,AEX,=6HOLD
 ,ASN,64+24
 ,U1A,NC1RET1.NOT OLD
 ,XTA,QCAR
 ,ARX,=7777777
 ,AAX,=7777777
 ,ATX,QCAR.QCAR=QCAR-1
 ,XTA,NOENDR
 ,UZA,NC1RET2.*ЗAKOHЧEH
 ,UJ,NAM11WC
 NC1RET1:,XTA,=1
 13,UJ,
 NC1RET2:,XTA,
 13,UJ,
C-----      OБPAБOTKA KAPTЫ PEЗYЛЬTATA
C      (CT. И HECT. CEPBИC)
C      ACARD-1-15 P.SYMM.
 NAMCALL2:,BSS,
 ,ATX,RR1
 ,ITA,13
 ,XTS,PROGON
 ,U1A,NLL2
 ,XTA,NOWRIT
 ,UIA,NCA.ECTЬ *U
 NLL2:,BSS,
 ,XTA,RABOTA1
 ,AAX,=7777 7777 7777 7774
 ,AOX,AWRITE
 ,UZA,NORA.HET PAБOTЫ
C      OБPAЩEHИE K CT. CEPBИCY
 ,XTA,RR1
 13,VJM,EDIT///
 ,ATI,13
 ,XTA,
 ,ATX,CAREAD
 ,UJ,COSY0.CHЯTИE COSY
 NCA:,XTA,47B.CПAC. ЛИCTA
 ,ATX,ID1.OБMEHA
 ,XTA,RM*+4
 ,ATX,47B
 ,WTC,RR1
 14,VTM,
 14,XTA,13.ИCПP.XBOCTA KAPTЫ
 ,AAX,=6H'377''377'''''''''
 ,AOX,=6H''''
 14,ATX,13
C      OБPAЩEHИE K HECT.CEPBИCY
 ,XTA,RR1
 14,VTM,EDIT///
 ,ITS,14
 ,XTS,NOWRIT
 ,CALL,LOADANGO
 ,ATI,13
 ,XTA,
 ,ATX,CAREAD
 ,XTA,ID1
 ,ATX,47B
 ,UJ,COSY0.CHЯTИE COSY
C--  --      CTAHДAPTHЫЙ CEPBИC
 EDIT///:,BSS,
 ,UTC,RR1
 ,ATX,.1-15-ACARD
 ,ITA,13
 ,ITS,6
 ,ITS,7
 14,VTM,XWRITE/.X-KA WRCARD
 14,XTS,
 ,ITS,
 7,VTM,BASE
 ,XTA,PROGON
 ,U1A,NAMJ
 14,XTA,
 ,AAX,=2000 0000 0000
 ,UIA,NWR/.COSY
 ,WTC,RR1
 14,VTM,
 14,XTA,13.BOCCT.'XBOCTA'
 ,AAX,=6H'377''377'''''''''
 ,AOX,=6H''''   '12'
 14,ATX,13
 NWR/:,BSS,
 ,XTA,CAREAD
 ,AEX,=:4
 ,UIA,NWR//.
 13,VJM,USEREND .
 ,UZA,NAMF.*READ OLD
 NWR//:,BSS,.
 ,XTA,RABOTA1
 ,AAX,=3
 ,UZA,NAMCAB.HET *N,*NI
 ,AEX,=1
 ,UZA,NAMCA10.*N
 ,XTA,CAREAD
 ,U1A,NAMCAB.KAPTA ИЗ *R
 NAMCA10:,BSS,
 ,XTA,RR1
 ,ATI,14
 ,XTA,NUMBERI.N=N(I)
 14,ATX,12
 ,XTA,NUMBERI+1
 14,ATX,13
 NAMCA1:8,VTM,XID
 9,VTM,NUMBERI
 10,VTM,2
 13,VJM,DO*LENT.ЛEHTA ДЛЯ N(I)
 ,WTC,STEP
 6,VTM,-7
 ,UJ,NAMCA2*
 NAMCA2:13,VJM,P1.+7
 NAMCA2*: 6,VLM,NAMCA2
 NAMCA3:13,VJM,RSIM.CИMBOЛ
 ,UTC,TABSIMIS
 14,XTA,
 ,AAX,=:7
 ,UZA,NAMCA4.БYKBA
 ,AEX,=:1
 ,UZA,NAMCA4.OГPAHИЧИTEЛЬ
 NAMCA3A:,ITA,14
 ,ARX,=1
 ,ATI,14
 ,AEX,=72.10
 ,UZA,NAMCA5
 13,VJM,WSIM
 ,UJ,NAMCAB
 NAMCA4:14,VTM,60B.ЗAMEHA HA 0
 ,UJ,NAMCA3A
 NAMCA5:14,VTM,60B
 13,VJM,WSIM.ЗAПИCЬ 0
 13,VJM,M1.-1
 11,VZM,NAMCA3
 NAMCAB:,BSS,
 ,XTA,RABOTA1
 ,AAX,=4
 ,UZA,NAMB.HET *LL
 13,VTM,NAMB
 ,UJ,PRIREZ.ПEЧATЬ KAPTЫ
 NAMB:,BSS,
 ,XTA,RABOTA1
 ,AAX,=10
 ,UZA,NAMC.HET *HL
 ,XTA,NAHL
 ,UIA,NAMY.ПOИCK END
 14,VTM,XREAD/
 14,XTA,1
 ,ASN,64+18
 ,ARX,AREAD
 ,ASN,64-36
 ,AUX,=6H'7''7''7''7'''''
 ,AOX,=6H0000
 ,ATX,NAMESUB+2.N ЗOHЫ
 13,VJM,COSYK.ПEPEXOД K OБЫЧHOMY ФOPMATY
 ,XTS,RR1
 ,CALL,CHKBEG*
 14,VIM,YF1.HAЙДEHO ИMЯ
 ,WTC,RR1
 ,XTA,
 ,ASN,64-8
 ,YTA,
 ,AEX,=52
 ,UIA,NAMC.KAPTA БEЗ *
 YF:13,VTM,NAMC
 ,UJ,PRIREZ
 YF1:,XTA,=1
 ,ATX,NAHL.ПP. ПOИCK END
 14,VTM,NAME*
 14,XTA,
 ,ATX,NAMESUB
 14,XTA,1
 ,AAX,=:7777 7777
 ,ATX,NAMESUB+1
 14,VTM,NAMESUB
 ,ITS,14
 14,UTM,2
 ,ITS,14
 14,VTM,1
 ,ITS,14
 13,VTM,YF
 ,UJ,PRINT8//.'ИMЯ--N ЗOHЫ
 NAMY:,XTS,RR1
 ,CALL,CHKEND*
 14,VZM,NAMC.HE END
 ,XTA,
 ,ATX,NAHL.'ПOИCK NAME'
 13,VTM,YF
 ,UJ,COSYK.OЧ. ПP.'COSY'
 NAMC:,XTA,RABOTA1
 ,AAX,=20
 ,UZA,NAMD.HE *LP
 ,WTC,RR1
 14,VTM,
 ,ITS,14
 ,CALL,COLPUNC*.PUNCH(*LP)
 NAMD:,BSS,
 ,XTA,RABOTA1
 ,AAX,=40
 ,UZA,NAMF.HE *CP
 ,WTC,RR1
 14,VTM,
 ,ITS,14
 13,VJM,FORMKOZA+1.PUNCH(*CP)
 NAMF:,BSS,
 ,XTA,AWRITE
 ,UZA,NAMJ.HET *W
 ,XTA,BL*R*W
 ,U1A,NAM2WC+1
 ,XTA,CAREAD
 ,AEX,AWRITE
 NAM2WC:,UZA,*EQRW.*W=*R
 ,XTA,
 ,XTA,CAREAD
 ,AEX,=:4.
C      =ПPИ *READ OLD ИЛИ *END FILE
 ,U1A,NAW
 13,VJM,USEREND.
 ,UIA,NAW.
 ,UTC,*TIKTAK*+24.
 ,XTA,.
 ,UIA,NAMJ.
 ,XTA,QCAR/
 ,UZA,NAMJ.QCAR/=0
 NAW:,BSS,
 ,XTS,RR1
 ,CALL,WRCARD
 ,XTA,CAREAD
 ,AEX,=:4
  ,UIA,NAW//.
 13,VJM,USEREND.
 ,UZA,NAMJ
 NAW//:,BSS,.
 14,VTM,XWRITE/
 14,XTA,1
 ,ASN,64+18
 ,ARX,=1
 ,AEX,AOGR
 ,AAX,=7777
 ,UZA,MB100*.>OГP.
 ,XTA,AWRITE
 ,ARX,=50 0000
 ,ASN,64+18
 ,U1A,NAMJJ.HE MБ
 14,XTA,1
 ,ASN,64+18
 ,AEX,=77
 ,AAX,=7777
 ,UZA,MB100*.>2-X MБ
 NAMJJ:,BSS,
 ,XTA,QCAR/
 ,ARX,=1
 ,ATX,QCAR/.QCAR/=QCAR/+1
 NAMJ:,BSS,
C      BOЗBPAT ИЗ CT.CEPBИCA
 ,STI,
 14,VTM,XWRITE/.CБPOC ПP.'COSY'
 ,AAX,=2000 0000 0000
 14,XTS,
 ,AOX,=2000 0000 0000
 ,AEX,=2000 0000 0000
 15,AOX,
 14,STX,
 ,STI,7
 ,STI,6
 ,STI,13
 13,UJ,
 USEREND:,XTA,RR1.
 ,ATI,14.A KAPTЫ
 14,XTA,.
 ,AEX,READ OLD
 ,UIA,USEN.HE *READ
 14,XTA,1.
 ,AEX,READ OLD+1
 13,UJ,.
 USEN:,BSS,.
 14,XTA,.
 ,AEX,END FILE
 13,UIA,.HE *END F
 14,XTA,1
 ,AEX,END FILE+1
 13,UJ,.
 MB100*:14,XTA,
 ,AAX,=1740
 ,AEX,=1740
 ,U1A,NAMJJ
 ,XTA,QCAR/
 ,ARX,=1
 ,ATX,QCAR/
 ,UJ,MB100
C---   ПEPEBOD KAPTЫ B OБ.ФOPMAT
 COSYK:14,VTM,XWRITE/
 14,XTA,
 ,AAX,=2000 0000 0000.ПP.'COSY'
 13,UZA,
 14,AEX,
 14,ATX,
 ,XTS,RR1
 ,UJ,COSYTISO
C------OKOHЧAHИE TAKE(ДOKPYTKA)
 DOKTAKE:,BSS,
 ,ITA,13
 ,ITS,
 ,XTA,NOENDTA
 ,UZA,DOKTARET.HET HEOK. TAKE
 ,XTA,
 ,ATX,ZAP
 ,ATX,PRBEF
 ,XTA,XNOENDTA
 13,VJM,POISK
 DOKTARET:,STI,
 ,ATI,13
 13,UJ,
C------OKOHЧAHИE *R(ДOKPYTKA)
 DOKREAD:,ITA,13
 ,XTS,
 ,XTA,PROGON
 ,U1A,DOKRET
 ,XTA,NOENDR
 ,UZA,DOKRET.HET HEOK. *R
 ,XTA,
 ,ATX,NOENDR
 ,XTA,YESCARD
 ,U1A,DOKRE1.KAPTA B MOЗY
 DOKRCOS:,BSS,
 13,VJM,DOCOSY.PEW.'COSY'
 14,VTM,CARDR
 ,ITA,14
 13,VJM,NAMCALL1.ЧTEHИE KAPTЫ
 ,UZA,DOKRET.READ OLD
 DOKRE1:,XTA,AREAD
 ,ATX,CAREAD
 14,VTM,CARDR
 ,ITA,14
 13,VTM,DOKRCOS
 ,UJ,NAMCALL2.ЗAПИCЬ KAPTЫ
 DOKRET:,BSS,
 ,XTA,NOLISFL*.
 ,UIA,DOKRET1.*NO LIST
 ,XTA,PFLAG.
 ,UZA,DOKRET1.HE БЫЛO *P,*PP
 ,ATI,14.
 14,UTC,.
 13,VJM,PRINT8//.
 DOKRET1:,XTA,.
 ,ATX,PFLAG.
 11,VTM,FILE
 14,VTM,XREAD/
 14,XTA,1
 ,ASN,64+18
 ,AAX,=7777
 ,ARX,=1
 ,ATX,QZT.K-BO ЗOH/TP.
 ,XTA,
 ,ATX,AREAD
 ,ATX,YRE
 13,VJM,COSY0.PEЖ.'HE COSY'
 14,VTM,DOKYR
 13,VJM,MYKA.ПEЧ.'CЧИTAHO...
 ,STX,
 ,ATI,13
 13,UJ,
C-----      OKOHЧAHИE *W(OФOPMЛEHИE
C      KOHЦA ФAЙЛA)
 DOKWRITE:,BSS,
 ,XTA,PROGON
 13,U1A,
 ,ITA,13
 ,XTS,NOWRIT
 ,AOX,AWRITE
 ,UZA,DOKW.HET *W ИЛИ *U
 ,XTA,=:4
 ,ATX,CREAD
 14,VTM,READOLD.*READ OLD
 ,ITA,14
 13,VJM,NAMCALL2
 ,XTA,=:4
 ,ATX,CREAD
 14,VTM,
 13,VJM,DOCOSY/.COSY
 14,VTM,ENDFILE
 ,ITS,14
 13,VJM,EDIT///
 ,XTA,QCAR/
 ,UZA,DOKWRIT.
 ,CALL,WRIEND
 DOKWRIT:,BSS,.
 13,VJM,COSY0
 ,XTA,
 ,ATX,CAREAD
 ,ATX,AWRITE
 11,VTM,FILE/
 14,VTM,XWRITE/
 14,XTA,1
 ,ASN,64+18
 ,AAX,=7777
 ,ATX,QZT/.K-BO ЗOH TP.
 14,VTM,DOKYW
 13,VJM,MYKA.ПЧ.'ЗAПИCAHO...
 DOKW:,BSS,
 ,STI,
 ,ATI,13
 13,UJ,
C-----      ПEЧATЬ CПPABOK 'CЧИTAHO...
C      'ЗAПИCAHO...
C      B I11 DOKYT ИЛИ DOKYW
 MYKA:,BSS,
 ,XTA,NOLISFL*
 13,UIA,.NO LIST
 11,XTA,QCAR-FILE
 13,UZA,
 ,ITA,13
 11,XTS,1
 14,ATX,1.FILE
 11,XTA,5
 14,ATX,8+1.ZONA-TRACT
 11,XTA,QZT-FILE
 ,ASN,64-36
 ,AUX,=6H'7''7''7''7'''''
 ,AOX,=6H0000''''
 14,ATX,7+1.QZT
 11,XTA,QCAR-FILE
 ,XTS,
 11,ATX,QZT-FILE
 11,STX,QCAR-FILE
 14,MTJ,11
 ,CALL,OCTTDEC.ПEPEBOД B 10
 ,AAX,=6H'17''17''17''17''17''17'
 15,ATX,
 ,ANX,=77777
 ,ASN,64+3
 ,ASN,64-3
 ,ATI,9
 ,XTA,=6H000000
 9,ASN,64
 15,AOX,
 ,AOX,=H
 11,MTJ,14
 14,ATX,4+1.QCAR
 ,ITS,14
 14,UTM,9
 ,ITS,14
 14,VTM,14
 ,ITS,14
 ,CALL,PRINT8//
 ,STX,
 ,ATI,13
 13,UJ,
C      ПEЧATЬ KAPTЫ:
C      YПP:PRAB+2=0,I12=0
 PRINTCD:,BSS,
 ,XTA,NOLISFL*
 13,U1A,.NO LIST
 PRI:,BSS,
C      KOПИЯ OПEPATOPH.ЧACTИ
 14,VTM,-11
 PRIC:14,XTA,CARD+11
 14,ATX,PRAB+14
 14,VLM,PRIC
C      BЫT-E HOMEPA BПEPEД
 ,XTA,CARD+12
 ,ATX,PRAB
 ,XTA,CARD+13
 ,AAX,=6H'377''377''377'''''''
 ,ATX,PRAB+1
 11,VTM,CARD
 ,XTA,LLPRI
 ,UZA,PRIC/.HE PEЗ.
 14,VTM,-11
 ,WTC,RR1
 11,VTM,.AДPEC RE3.
C      KOПИЯ OПEPATOPH. ЧACTИ
 PRICA:11,UTC,
 14,XTA,11
 14,ATX,PRAB+14
 14,VLM,PRICA
C      BЫHOC HOMEPA BПEPEД
 11,XTA,12
 ,ATX,PRAB
 11,XTA,13
 ,AAX,=6H'377''377''377'''''''
 ,ATX,PRAB+1
 ,XTA,
 ,ATX,LLPRI.ЧИCTKA
 PRIC/:,BSS,
 ,XTA,*REGIM//
 ,AAX,=:7
 ,AEX,=:6
 ,UZA,PRIC//.БEЗ БИЛИCTA
 ,XTA,LTPR1.
 ,UIA,PRICLT.
 ,ITA,13
 14,VTM,PRAB
 ,ITS,14
 14,VTM,PRAB+11
 ,ITS,14
 ,XTS,
 ,CALL,PRINT8//.1-Я CTPOKA
 ,ATI,13
 14,VTM,-2
 PRIC1:14,XTA,PRAB+14
 ,AEX,=H
 ,U1A,PRIC2.ECTЬ 'XBOCT'
 14,VLM,PRIC1
 13,UJ,
 PRIC2:14,VTM,PRAB+12
 ,ITS,14
 14,VTM,PRAB+14
 ,ITS,14
 14,VTM,45
 ,ITS,14
 ,UJ,PRINT8//.ПEЧATЬ XBOCTA
 PRIC//:,ITS,11
 11,UTM,13
 ,ITS,11
 ,ITS,
 ,UJ,PRINT8//
 PRICLT:14,VTM,PRAB+3.
 14,XTA,10.
 ,ASN,64+24.
 ,ASN,64-24.
 ,AOX,=6H'0''0''0'
 14,ATX,10.ЧИCTKA 64-66 CИMBOЛOB
 ,ITS,14.
 14,UTM,10.
 ,ITS,14.
 ,XTS,.
 ,ATX,LTPR1.
 ,UJ,PRINT8//.
C      ПEЧATЬ HEYПPABЛЯЮЩEЙ KAPTЫ:
C      PRAB+2=2ПPOБEЛA,I12=2
 PRINOYP:,BSS,
 ,XTA,YAR
 13,UIA,.ECTЬ *Y
C      ПEЧATЬ KAPTЫ-PEЗYЛЬTATA:
 ,UJ,PRINTCD
 PRIREZ:,BSS,
 ,XTA,NOLISFL*
 13,U1A,
 ,XTA,PROGON
 13,UIA,.БЫЛИ OШИБKИ
 ,XTA,LTPR.
 ,ATX,LTPR1.
 ,XTA,=1
 ,ATX,LLPRI
 ,UJ,PRINTCD
C------OБPAБOTKA ПAPAMETPOB KAPTЫ:
C       *YC:N1,N2
 PARA:,BSS,
 ,ITA,1.K
 ,ITS,2.K'
 ,ITS,13
 ,ITS,
 ,ATX,N2.N2=0
 11,V1M,SLMIST.HET :
 PAR:,ATX,ZAP.
 8,VTM,XID
 9,VTM,IDID
 10,VTM,12
 13,VJM,DO*LENT.ЛEHTA CИMBOЛOB
 1,VTM,.K=0
 PAR2:2,VTM,.K'=0
 PAR3:8,VTM,XCARD
 13,VJM,P1*R.ЧTEHИE CИMBOЛA
 11,V1M,PARFIN.73-Й CИMBOЛ
 14,MTJ,13
 13,UTM,-54B
 13,V1M,PAR4.HE ,
 ,XTA,ZAP
 ,U1A,SLMIST.ПOCЛE N2
 1,VZM,SLMIST.HET N1
 13,VJM,MADENUM.OБPAБOTKA N1
 ,XTA,=1
 ,UJ,PAR
 PAR4:8,VTM,XID
 13,VJM,W*P1.ЗAПИCЬ CИMBOЛA
 14,UTM,-40B
 14,V1M,PAR5.HE ПPOБEЛ
 2,UTM,1.K'=K'+1
 ,UJ,PAR3
 PAR5:2,J+M,1.K=K+K'
 1,UTM,1.K=K+1
 ,UJ,PAR2
 PARFIN:1,VZM,SLMIST.HET N2
 13,VJM,MADENUM.OБPAБOTKA N2
 ,STI,
 ,STI,13
 ,STI,2
 ,ATI,1
 13,UJ,
C---   OБPAБOTKA N1 ИЛИ N2
 MADENUM:,BSS,.
 ,ITA,13
 ,XTS,
 ,ATX,RR2
 1,MTJ,2
 2,UTM,-1
 ,ITA,2
 ,ASN,64+3
 ,U1A,SLMIST.>8 CИMBOЛOB
 2,UTM,-7
 2,V1M,MADEN.HE=8
C      8 CИMBOЛOB B PEЗYЛЬTAT
 ,XTA,ID1
 ,ATX,RR1
 ,XTA,ID2
 ,AAX,=:777774
 ,ATX,RR2
 ,UJ,MADEN2/
C      ПEPEHOC CTAPШИX ИЗ ЭTAЛOHA
 MADEN:1,MTJ,2
 2,UTM,-8
 2,UTM,1.-(8-K-1)
 8,VTM,XID
 9,VTM,NETAL
 10,VTM,2
 13,VJM,DO*LENT.ЛEHTA ЭTAЛOHA
 8,VTM,XID1
 9,VTM,RR1
 10,VTM,2
 13,VJM,DO*LENT.ЛEHTA ДЛЯ N
 MADEN1:8,VTM,XID.ПEPEHOC CTAPШИX
 13,VJM,R*P1
 8,VTM,XID1
 13,VJM,W*P1
 2,VLM,MADEN1.8-K
 8,VTM,XID
 9,VTM,IDID.
 10,VTM,2
 13,VJM,DO*LENT
 MADEN2:8,VTM,XID.ПEPEHOC MЛAДШИX
 13,VJM,R*P1
 8,VTM,XID1
 13,VJM,W*P1
 1,UTM,-1
 1,V1M,MADEN2
 MADEN2/:,BSS,
 ,XTA,ZAP
 ,U1A,MADEN3.HE N1
C      ДOOФOPMЛEHИE N1
 ,XTA,RR1
 ,ATX,N1
 ,ATX,NETAL
 ,XTA,RR2
 ,AEX,=6H''''   '12'
 ,ATX,N1+1
 ,ATX,NETAL+1
 ,UJ,MADEN4
C      ДOOФOPMЛEHИE N2
 MADEN3:,XTA,RR1
 ,ATX,N2
 ,XTA,RR2
 ,AEX,=6H''''   '12'
 ,ATX,N2+1
 MADEN4:,STI,
 ,ATI,13
 13,UJ,
C------OБPAБOTKA HEYПPABЛЯЮЩEЙ KAPTЫ
 NOYPR:,BSS,
 ,XTA,CARD
 ,AEX,=6H )))
 ,ASN,64-8
 ,U1A,NOYY
 ,XTA,CARD
 ,ASN,64+40
 ,AEX,FOR*
 ,UIA,NOYY.HE *)))
 ,XTA,BLYPR
 ,UZA,SLMIST.HET БЛ-KИ YПPKAPT
 ,AAX,=:1
 ,UZA,SLMIST.HE БЫЛO *(((
C      YЧET YPOBHЯ*(((-*)))
 ,XTA,YROV
 ,ARX,=77777
 ,AAX,=77777
 ,ATX,YROV.YROV=YROV-1
 ,XTA,YROV
 ,UIA,NOYPR1.HE 0
 ,ATX,BLYPR.CHЯTИE БЛ-KИ
 ,UJ,SLCARD
 NOYY:,BSS,
 ,XTA,WWW
 ,UIA,NOYPR/.ECTЬ PEД-E /
 ,XTA,BLYPR
 ,UZA,NOYPR1.HET БЛ-KИ YПP.
 ,AAX,=:1
 ,UIA,NOYPR1.БЫЛA *(((
C      KOPPEKЦИЯ ЧИCЛA HEYПP.
 ,XTA,BLYPR
 ,ATI,13
 13,UTM,-1
 ,ITA,13
 ,ATX,BLYPR.Q=Q-1
 NOYPR1:,BSS,.KAPTY HA CEPBИC
 ,XTA,PROGON
 ,U1A,SLCARD
 13,VTM,CARD
 ,ITA,13
 13,VJM,NAMCALL2
 13,VTM,SLCARDN
 ,UJ,PRINOYP
 NOYPR/:,BSS,
 ,XTA,PROGON
 ,U1A,SLCARD
 13,VJM,RSIM
 14,UTM,-40B
 14,VZM,*L/.ПEPBЫЙ-ПPOБEЛ
 14,VTM,9
 ,UJ,MISTAKE
C------ ПOИCK KAPTЫ ИЗ ФAЙЛA ЧTEHИЯ
C      1-15 P.SYMM-XAPAKTEPИCTИKA:
C      48-46 I,30-16 X2,15-1 X1
C      ПO HOMEPY:I=0,X1=AДPEC N
C      ПO KOЛ-BY:I=1,X1=Q
C      ПO OБPAЗY:I=2,X1=ACARD,X2-X-KA ЭT-HA
C      ПO ИMEHИ :I=3,X1=ANAME
C      ПO END  :I=4
C      ----ZAP=0-ЗAПИCЬ PAЗPEШEHA
C      ----PRBEF HE 0-BEFPRE
C      ZAP=0-ЗAПИCЬ PAЗPEШEHA
C      PRBEF NEQ 0-BEFORE
 POISK:,BSS,
 ,ATX,RRY
 ,XTA,PROGON
 13,U1A,
 ,ITA,13
 ,XTS,YESCARD
 ,U1A,POI1.ECTЬ KAPTA
 POI:,BSS,
 13,VJM,COSY0.HE COSY
 ,XTA,RRY
 ,AEX,=:4.POISK END TN
 ,UZA,POIMY
 ,AEX,=:7.POISK NAME
 ,AAX,=:7
 ,U1A,POICONT
 ,XTA,NAEM
 ,UZA,POICONT.ИЩETCЯ NAME
 ,XTA,TOUT
 ,AOX,BLTAKE
 ,UIA,POIMY.БЫЛA *TO ИЛИ *BT
 14,VTM,XREAD/
 13,VTM,POICONT
C      COSY B ЛЮБOM CЛYЧAE
 ,UJ,DOCOSY/
C      COSY B HEПPOTИBOP. CЛ-X
 POIMY:13,VJM,DOCOSY
 POICONT:14,VTM,CARDR
 ,ITA,14
 13,VJM,NAMCALL1.ЧTEHИE KAPTЫ
 POI1:,XTA,RRY
 13,VJM,YZNAL
 ,UZA,POI10.KAPTY YЗHAЛИ
 ,XTA,RRY
 ,AEX,=:4
 ,UZA,POI2.ПOИCK END
 ,XTA,NOENDTA
 ,UZA,POI2.HET HEЗAK.TAKE
 ,XTA,XNOENDTA.X-KA TAKE
 13,VJM,YZNAL
 ,U1A,POI2.HE END TAKE
 ,ATX,NOENDTA
 ,XTA,TOUT
 ,UZA,NAM11WC
 POI2:,XTA,ZAP
 ,U1A,POI.БЛ.ЗAПИCИ
  :,XTA,AREAD.'KAPTA ИЗ *R'
 ,ATX,CREAD
 14,VTM,CARDR
 ,ITA,14
 13,VTM,POI
 ,UJ,NAMCALL2.CEPBИC
 POI10:,XTA,PRBEF
 ,UZA,POI11.HE BEFORE
 ,ATX,YESCARD.ПP.'ECTЬ KAPTA
 ,UJ,POIFIN.HA BOЗBPAT
 POI11:,ATX,YESCARD.ПP.'HET KAPTЫ'
 ,XTA,NOENDTA
 ,UZA,POI12.HET HEЗAK.TAKE
 ,XTA,RRY
 ,AEX,=:4
 ,UZA,POI11/.ПOИCK END
 ,XTA,XNOENDTA.X-KA TAKE
 13,VJM,YZNAL
 POI11/:,BSS,
 ,ATX,NOENDTA
 ,ATX,NOENDR
 ,U1A,POI12.HE END
 ,XTA,BLTAKE
 ,AOX,TOUT
 ,ATX,NOENDR
 ,XTA,ZAP
 ,U1A,POI14.БЛ-KA ЗAП
 ,XTA,AREAD
   ,ATX,CREAD.'KAPTA ИЗ *R'
 14,VTM,CARDR
 ,ITA,14
 13,VJM,NAMCALL2.CEPBИC
 POI14:,XTA,TOUT
 ,U1A,POIFIN.ECTЬ *TO
 ,XTA,RABOTA2.OKOHЧAHИE:
 13,VJM,ENDLI.LIST
 ,XTA,RABOTA2
 13,VJM,ENDPU.PUNCH
 ,XTA,RABOTA2
 13,VJM,ENDUZ.USER
 ,XTA,RABOTA1
 ,AEX,RABOTA2
 ,ATX,RABOTA1
 ,XTA,
 ,ATX,RABOTA2
 ,UJ,POIFIN
C---   ЧИCTKA OTД.ПP-KOB B ШKAЛAX
C      PAБOT(I-12-MACKA)
 RBT:,BSS,
 ,ITA,12
 ,AAX,RABOTA1
 ,AEX,RABOTA1
 ,ATX,RABOTA1
 ,ITA,12
 ,AAX,RABOTA2
 ,AEX,RABOTA2
 ,ATX,RABOTA2
 13,UJ,
 POI12:,XTA,ZAP
 ,U1A,POIFIN.HET ЗAПИCИ
 ,XTA,BLARECRE
 ,U1A,*+2
 ,XTA,AREAD
 ,ATX,CREAD
 14,VTM,CARDR
 ,ITA,14
 13,VJM,NAMCALL2.CEPBИC
 POIFIN:,STX,.BOЗBPAT
 ,ATI,13
 13,UJ,
C----  CPABHEHИE OЧEPEДHOЙ KAPTЫ
C      C ИCKOMOЙ
C      SYMM-X-KA(KAK Y POISK)
C      REZ:SYMM=0-COBПAДEHИE
 YZNAL:,BSS,
 ,ATX,RR1.X-KA
 ,ITA,13
 ,XTS,RR1
 ,AAX,=:7
 ,ASN,64-3
 ,YTA,
 ,ATI,14
 14,UJ,YZNPER.HA ПEPEKЛ.
 YZNPER:,UJ,YZN.ПO HOMEPY
 :,UJ,YZN*Q.ПO Q
 :,UJ,YZN*OB.ПO OБPAЗY
 :,UJ,YZN*NA.NAME
 :,UJ,YZN*EN.END
 YZN*Q:,BSS,
 ,XTA,RR1
 ,AAX,=77777
 ,UZA,YZNF0.Q=0
 ,ARX,=77777
 ,AAX,=77777
 ,AOX,=:1
 ,ATX,RRY.Q=Q-1
 ,UJ,YZNF1
 YZN*OB:,BSS,
 8,VTM,XID
 ,WTC,RR1
 9,VTM,
 10,VTM,12
 13,VJM,DO*LENT.ЛEHTA KAPTЫ
 ,ITA,8
 ,XTS,RR1
 ,AAX,=77777 00000
 15,AEX,.X-KA ДЛЯ YZOB
 13,VJM,YZOB.CPABHEHИE
 ,UZA,YZNF0.COBПAДEHИE
 ,UJ,YZNF1
 YZN*NA:,BSS,
 ,XTA,NAEM
 ,UIA,YNE.ИЩETCЯ END
 14,VTM,CARDR
 ,ITS,14
 ,CALL,CHKBEG*.OПP-E ИMEHИ
 14,VZM,YZNF1.HET ИMEHИ
 ,WTC,RR1
 14,VTM,
 14,XTA,
 13,VTM,NAME*
 13,AEX,
 ,UIA,YNA.HECOBПAД ИMEH
 14,XTA,1
 13,AEX,1
 ,AAX,=:777774
 ,UZA,YZNF0.COBПAД ИMEH
 YNA:,ATX,NAEM
 ,UJ,YZNF1
 YNE:14,VTM,CARDR
 ,ITS,14
 ,CALL,CHKEND*
 14,VZM,YZNF1.HE END
 ,XTA,
 ,UJ,YNA.END
 YZN*EN:,BSS,
 14,VTM,CARDR
 ,ITS,14
 ,CALL,CHKEND*.OПP-E END
 14,VZM,YZNF1.HE END
 ,XTA,
 ,ATX,NAEM
 ,UJ,YZNF0.END
 YZN:,XTA,RR1
 ,ATI,14
 14,XTA,
 ,AEX,CARDR+12
 ,U1A,YZNF1.HECOBП.
 14,XTA,1
 ,AEX,CARDR+13
 ,U1A,YZNF1.HECOBП
 YZNF0:,BSS,.YЗHAЛИ
 ,STX,
 ,ATI,13
 ,XTA,
 13,UJ,
 YZNF1:,STX,.HE YЗHAЛИ
 ,ATI,13
 ,XTA,=1
 13,UJ,
C---   ПEPEXOД K COSY(*R,*W)
 DOCOSY:,BSS,
 ,XTA,RABOTA1
 14,VTM,XREAD/
 ,AAX,=7777 7777 7777 7765
 ,U1A,DOUS
 DOCOSY/:,BSS,
 14,XTA,
 ,AOX,=2000 0000 0000
 14,ATX,
 14,VTM,XWRITE/
 14,XTA,
 ,AOX,=2000 0000 0000
 14,ATX,
 13,UJ,
 DOUS:,AEX,=100
 13,U1A,
 14,XTA,
 ,AAX,=4000 0000 0000
 13,UZA,
 ,UJ,DOCOSY/
C---   KOHTPOЛЬ-KOPP. ПOPЯДKA
C      YПP.KAPT
 PROVE:,BSS,
 ,XTA,TOUT
 13,UIA,.БЫЛA *TO
 ,XTA,YESTAKE
 13,UZA,.HE БЫЛO TAKE
 ,XTA,NOENDTA
 ,UZA,NAM11WC
 13,UJ,
C---   CPABHEHИE KAPTЫ C OБPAЗOM
C      SYMM=30-16-X-KA OБPAЗA,
C           15-1-X-KA KAPTЫ
C      REZ:SUMM=0,Q72=0 COBПAДEHИE
C          YO5-K-BO CИMB.B OБPAЗE
 YZOB:,BSS,
 ,ATX,YO1.X-KA
 ,ITA,13
 ,ITS,8
 ,ITS,2
 ,ITS,1
 ,XTS,YO1
C      CПACAHИE X-K ЛEHT
 ,ATI,8
 9,VTM,SX1
 13,VJM,L1*TO*L2
 ,XTA,YO1
 ,ASN,64+15
 ,ATI,8
 9,VTM,SX2
 13,VJM,L1*TO*L2
 1,VTM,
 2,VTM,
 ,XTA,
 ,ATX,YO5
 ,XTA,=1
 ,ATX,YO2.ПP.'HECOBП.'
 YZO:8,VTM,SX2
 13,VJM,RSIM
 ,ITA,14
 ,ATX,YO3.S2
 14,UTM,-40B
 14,VIM,YZO1*.S2 NOT BLANK
 YZOA:13,VJM,P1
 11,VZM,YZO
 YZOA*:,BSS,
 1,VZM,ERRBL.HET OБPAЗA
 ,XTA,
 ,ATX,YO2.ПP. 'COBП-E'
 ,UJ,YZOFIN
 YZO1*:14,UTM,-17B
 14,V1M,YZO1
 ,XTA,RR1*
 ,U1A,YZOA*
 YZO1:8,VTM,SX1
 2,VZM,YZO2
 :13,VJM,P1
 11,VIM,YZOFI.HECOBП-E
 YZO2:1,VTM,1
 2,VTM,1
 13,VJM,RSIM
 ,XTA,YO5.KOЛИЧECTBO
 ,ARX,=1.CИMBOЛOB
 ,ATX,YO5.B OБPAЗE
 ,ITA,14
 ,ATX,YO4.S1
 14,UTM,-40B
 14,VIM,YZO3.S1 NOT BLANK
 ,UJ,YZO1+1
 YZO3:,XTA,YO3
 ,AEX,YO4
 ,UIA,YZOFI.S1 NEQ S2
 8,VTM,SX2
 ,UJ,YZOA
 YZOFIN:11,VTM,
 YZOFI:,ITA,11
 ,STX,Q72
 ,STI,1
 ,STI,2
 ,STI,8
 ,ATI,13
 ,XTA,YO2
 13,UJ,
C---   ФOPMИPOBAHИE XAPAKTEPИCTИKИ POISK
C      ДЛЯ ПOИCKA OБPAЗA(RR1-REZ)
 LENT0:,BSS,
 ,ITA,13
 ,XTS,
 8,VTM,XCARD
 13,VJM,P1
 11,VIM,SLMIST.HET OБPAЗA
 ,ITA,8
 ,ASN,64-15
 13,VTM,CARDR
 ,ITS,13
 15,AEX,
 ,AEX,=:2
 ,STX,RR1
 ,ATI,13
 13,UJ,.BLANK
C---   ФOPMИPOBAHИE XAPAKTEPИCTИKИ
C      ДЛЯ ПOИCKA ПO Q(RR1-REZ)
 YKAZQ:,BSS,
 ,ITA,13
 ,XTS,
 8,VTM,XCARD
 ,ATX,FNNN.Q=DI=0
 YQ:13,VJM,P1*R
 11,VIM,YQFIN
 14,MTJ,13
 13,UTM,-40B
 13,VZM,YQ
 ,UTC,TABSIMIS
 14,XTA,
 ,AAX,=:7
 ,UZA,SLMIST.БYKBA
 ,AEX,=:1
 ,UZA,SLMIST.OГPAHИЧ-ЛЬ
 ,XTA,FNNN
 ,ASN,64-8
 ,ITS,14
 15,AOX,
 ,ATX,FNNN
 ,UJ,YQ
 YQFIN:,XTA,FNNN
 ,UZA,SLMIST.Q=0
 ,ASN,64+16
 ,UIA,SLMIST.Q>99
 ,XTA,FNNN
 ,AAX,=17.I
 ,XTS,FNNN
 ,ASN,64+8
 ,AAX,=17.D
 ,ASN,64-1.D*2
 15,ATX,
 ,ASN,64-2.D*8
 15,ARX,.D*10
 15,ARX,.Q=D*10+I
 ,ARX,=77777
 ,AAX,=77777.Q=Q-1
 ,AEX,=:1.ПP.'ПOИCK Q'
 ,STX,RR1
 ,ATI,13
 13,UJ,
C------ПOИCK OБPAЗA HA KAPTE
C      SYMM-XAPAKTEPИCTИKA:
C      48-46 I,15-1 A
C      ПO KOЛ-BY:I=0,A=Q
C      ПO OБPAЗY:I=2,A=AДP.X-KИ
C      REZ:ECTЬ OБPAЗ:BOЗBPAT
 POISK/:,BSS,
 ,ATX,RRY
 ,XTA,PROGON
 13,U1A,
 ,ITA,13
 ,XTS,
 ,XTA,WBLAN
 ,UIA,ERRBLT.БЫЛA *DF
 ,XTA,RRY
 ,AAX,=:7
 ,UIA,PO/2.NOT Q
 PO/:,XTA,RRY
 ,UZA,PO/FIN.Q=0
 ,XTA,Q72
 ,UIA,ERRNO/.HET OБPAЗA
 8,VTM,SX1/
 13,VJM,R*P1.ЧTEHИE CИMBOЛA
 ,ITA,11
 ,ATX,Q72
 ,XTA,ZAP
 ,U1A,PV1.HET ЗAПИCИ
 PV:,BSS,
 8,VTM,SXR/.REZ>72
 ,XTA,W72
 ,UIA,PO/MI
 13,VJM,W*P1
 ,ITA,11
 ,ATX,W72
 PO/0:,XTA,RRY
 ,ARX,=77777
 ,AAX,=77777
 ,ATX,RRY.Q=Q-1
 ,UJ,PO/
 PV1:,XTA,ZAP
 ,AEX,=:1
 ,U1A,PO/0.*D
 14,VTM,40B.BLANK
 ,UJ,PV.*V
 PO/FIN:,STX,.BOЗBPAT
 ,ATI,13
 13,UJ,
 PO/2:8,VTM,SX1/
 13,VJM,RSIM.ЧTEHИE CИMBOЛA
 ,XTA,Q72
 ,U1A,ERRNO/.HET OБPAЗA
 14,UTM,-40B
 14,VZM,PO/2BL.BLANK
 ,XTA,RRY
 ,ASN,64-15
 ,ITS,8
 15,AEX,
 13,VJM,YZOB.X-KA
 ,UZA,PO/EQ.CP-E OБPAЗOB
 PO/2BL:,BSS,.COBПAЛИ
 ,XTA,Q72
 ,UIA,ERRNO/.HET OБPAЗA
 8,VTM,SX1/
 13,VJM,R*P1.ЧTEHИE CИMBOЛA
 ,ITA,11
 ,ATX,Q72
 ,XTA,ZAP
 ,U1A,PV*1.HET ЗAПИCИ
 PV*:,BSS,
 8,VTM,SXR/
 ,XTA,W72
 ,UIA,PO/MI.>72
 13,VJM,W*P1.ЗAПИCЬ CИMBOЛA
 ,ITA,11
 ,ATX,W72
 ,UJ,PO/2
 PV*1:,XTA,ZAP
 ,AEX,=:1
 ,U1A,PO/2.*D
 14,VTM,40B.BLANK
 ,UJ,PV*
 PO/EQ:,BSS,
 ,XTA,PRBEF
 ,UIA,PO/FIN.BEFORE
 ,XTA,YO5
 ,ATX,RRY.Q=YO5
 ,ATX,PRBEF
 ,UJ,PO/
C---   OПPEДEЛEHИE ДЛИHЫ OБPAЗA
C      HA YПPABЛЯЮЩEЙ KAPTE,
C      ПOДBOД K KAPTE *R,
C      KOHTPOЛЬ ПOPЯДKA OПEPAЦИЙ
C      LONG=Q
C      RRY-X-KA ДЛЯ POISK/
 LONG/:,BSS,
 ,ITA,13
 ,XTS,WWW
 ,UIA,LO/A.ПOДBOД HE HYЖEH
 ,ATX,Q72.Q72=0
 ,XTA,=:1
 ,ATX,PRBEF.Q=0
 13,VJM,POISK.ПOДBOД
 LO/A:,XTA,=1
 ,ATX,WWW
 13,VJM,PROVE.KOHTPOЛЬ
 8,VTM,XCARD
 13,VJM,P1
 9,VTM,X/CARD
 13,VJM,L1*TO*L2.KOПИЯ X-KИ
 8,VTM,X/CARD
 ,XTA,
 ,ATX,RR1*
 ,ATX,BLYPR
 ,ATX,LONG.Q=0
 ,ATX,LOBL.Q'=0
 LO/:13,VJM,R*P1
 14,UTM,-40B
 14,VIM,LO/1.NOT BLANK
 ,XTA,LOBL
 ,ARX,=1
 ,ATX,LOBL.Q'=Q'+1
 ,UJ,LO/2
 LO/1:14,UTM,-17B
 14,V1M,LO/1A.NON /
 ,XTA,DVDV
 ,AEX,=:1
 ,UZA,LO/B
 LO/1A:,XTA,LONG
 ,ARX,=1
 ,ARX,LOBL
 ,ATX,LONG.Q=Q+1+Q'
 ,XTA,
 ,ATX,LOBL.Q'=0
 LO/2:11,VZM,LO/.<73
 11,VTM,
 8,VTM,XCARD
 ,ITA,8
 ,AEX,=:1
 ,STX,RRY.X-KA POISK/
 ,ATI,13
 13,UJ,
 LO/B:11,V1M,LO/2
 13,VJM,M1
 8,VTM,X/CARD
 9,VTM,XID
 13,VJM,L1*TO*L2
 13,VJM,P1
 LO/C:11,V1M,ERRBL
 13,VJM,R*P1
 14,UTM,-40B
 14,VZM,LO/C.BLANK
 ,XTA,=1
 ,ATX,DVDV
 ,ATX,RR1*
 11,VTM,1
 ,UJ,LO/2
C---   ЗAПИCЬ(INSERT) OБPAЗA
 VSTAV/:,BSS,
 ,XTA,PROGON
 13,U1A,
 ,ITA,13
 ,XTS,
 VST/:,XTA,LONG.ДЛИHA OБP.
 ,UZA,VST/FIN.Q=0
 ,ARX,=77777
 ,AAX,=77777
 ,ATX,LONG.Q=Q-1
 8,VTM,XCARD
 13,VJM,R*P1
 ,XTA,W72
 ,UIA,PO/MI.>73
 8,VTM,SXR/
 13,VJM,W*P1.ЗAПИCЬ CИMBOЛA
 ,ITA,11
 ,ATX,W72
 ,UJ,VST/
 VST/FIN:,STX,.BOЗBPAT
 ,ATI,13
 13,UJ,
C---   ДOПOЛHEHИE PEЗYЛЬTATA
C      PEД-Я / ПPOБEЛAMИ
 DOKR/B:,ITA,13
 ,XTS,
 DOBL/:,XTA,W72.>72
 ,UIA,DOKR/F
 14,VTM,40B
 8,VTM,SXR/
 13,VJM,W*P1
 ,ITA,11
 ,ATX,W72
 ,UJ,DOBL/
C------OKOHЧAHИE PEДAKTИPOBAHИЯ /
 DOKR/:,BSS,
 ,XTA,WWW
 ,ATX,WWW/.KOПИЯ WWW
 13,UZA,.PEД-E ЗAKOHЧEHO
 ,XTA,WBLAN
 ,UIA,DOKR/B.БЫЛA *DF
 ,ITA,13
 ,XTS,
 13,VTM,DOBL/.ON BLANKING
 ,UJ,POISKF/.(*IF)
 DOKR/F:13,VTM,-11.PEЗYЛЬTAT
 :13,XTA,11+SCARD.PEДAKTИPOBAHИЯ
 13,ATX,CARDR+11./ B ИCXOДHYЮ
 13,VLM,DOKR/F+1.KAPTY
C      ЛEHTЫ ДЛЯ PEД-Я /
 8,VTM,SX1/
 9,VTM,CARDR
 10,VTM,12
 13,VJM,DO*LENT
 8,VTM,SXR/
 9,VTM,SCARD
 10,VTM,12
 13,VJM,DO*LENT
C      ЧИCTKA
 ,XTA,=1
 ,ATX,BLARECRE
 ,XTA,
 ,ATX,WWW
 ,ATX,WBLAN
 ,ATX,Q72
 ,ATX,W72
 ,ATX,ZAP
 ,ATX,PRBEF
 ,AEX,=:1
 13,VJM,POISK.ЗAXOД ЗA KAPTY
 ,XTA,PROGON.
 ,UIA,DOKR/E.
 ,XTA,YAR
 ,UIA,DOKR/E.БЫЛA *Y
 13,VJM,PRIREZ.ПЧ REZ
 DOKR/E:,BSS,
 ,XTA,
 ,STX,BLARECRE
 ,ATI,13
 13,UJ,
C---   ПOДBOД ЗA ПOCЛEДHИЙ
C      NONBLANK SYMBOL
 POISKF/:,BSS,
 ,XTA,PROGON
 13,U1A,
 ,ITA,13
 ,XTS,Q72
 ,UIA,POF/F.>72
 8,VTM,SX1/
 9,VTM,X/CARD
 13,VJM,L1*TO*L2.KOПИЯ X-KИ
 8,VTM,X/CARD
C      ПOДBOД K 73-MY
 POF/:13,VJM,P1
 11,VZM,POF/
C      ЗAMEHA ПPOБEЛOB XBOCTA
C      HYЛЯMИ
 POF/1:13,VJM,M1*R
 11,VIM,POF/2.<1
 14,UTM,-40B
 14,VIM,POF/2.NONBLANK
 14,VTM,
 13,VTM,POF/1
 ,UJ,WSIM.ЗAПИCЬ 0
 POF/2:8,VTM,SX1/
 ,XTA,Q72
 ,UIA,POF/F
 13,VJM,RSIM
 14,VZM,POF/BL.SIM=0
 8,VTM,SXR/
 ,XTA,W72
 ,UIA,PO/MI.>72
 13,VJM,W*P1.ЗAПИCЬ
 ,ITA,11
 ,ATX,W72
 8,VTM,SX1/
 13,VJM,P1.+1
 ,ITA,11
 ,ATX,Q72
 ,UJ,POF/2
C      ЗAMEHA HYЛEЙ ПPOБEЛAMИ
 POF/BL:9,VTM,X/CARD
 13,VJM,L1*TO*L2.KOПИЯ X-KИ
 8,VTM,X/CARD
 14,VTM,40B.BLANK
 POF/B:,BSS,
 11,V1M,POF/F
 13,VTM,POF/B.ПPOБEЛAMИ
 ,UJ,W*P1
 POF/F:,STX,.BOЗBPAT
 ,ATI,13
 13,UJ,
C------OБPAБOTKA NNN-KOЛИЧECTBA
C      HEYПPABЛЯЮЩИX KAPT,
C      CЛEДYЮЩИX ЗA ДAHHOЙ
 TONNN:,BSS,
 ,XTA,NNN.NNN
 13,UZA,
 ,ASN,64+16
 ,U1A,SLM.>99
 ,XTA,NNN.NNN=NK
 ,AAX,=17.K
 ,XTS,NNN
 ,ASN,64+8
 ,AAX,=17.N
 ,ASN,64-1.N*2
 15,ATX,
 ,ASN,64-2.N*8
 15,ARX,.N*10
 15,ARX,.N*10+K
 SLM1: ,ATX,BLYPR
 ,ATX,NNN
 ,XTA,BLYPR
 ,UZA,SLMIST.NNN=0
 13,UJ,
 SLM:,XTA,
 ,UJ,SLM1
C---   ФOPMИPOBAHИE ПPИЗHAKOB CEPBИCA
C      ПO MACKE B I12-CHЯTИE
C      ПO MACKE B I14-ПOCTAHOBKA
 RBT0:,BSS,
 ,ITA,12
 ,AAX,RABOTA1
 ,AEX,RABOTA1
 ,ITS,14
 15,AOX,
 ,ATX,RABOTA1
 ,XTA,NOENDTA
 13,UZA,.HET HEOK. TAKE
 ,ITA,12
 ,AAX,RABOTA2
 ,AEX,RABOTA2
 ,ITS,14
 15,AOX,
 ,ATX,RABOTA2
 13,UJ,
C------OБPAБOTKA *N
 *N:,BSS,
 14,VTM,1.ПP.*N
 NUM*:12,VTM,3
 13,VJM,RBT0
 11,VIM,SLMIST.HET YKAЗATEЛЯ
 8,VTM,XID
 9,VTM,NUMBERI
 10,VTM,2
 6,VTM,-7
 13,VJM,DO*LENT.ЛEHTA CИMBOЛOB
C      OФOPMЛEHИE HOMEPA B NUMBERI
 NUM:8,VTM,XCARD
 13,VJM,P1*R
 8,VTM,XID
 13,VJM,W*P1
 6,VLM,NUM.8 CИMBOЛOB
 14,UTM,-40B
 14,V1M,SLCARD
 13,VJM,M1.-1
 14,VTM,60B
 13,VTM,SLCARD
 ,UJ,WSIM
C------OБPAБOTKA *NI
 *NI:,BSS,
 14,VTM,2
 ,UJ,NUM*
C      OБPAБOTKA *NS
 *NS:,BSS,
 8,VTM,XCARD
 1,VTM,.S=0
 *NS1:11,V1M,*NS3
 13,VJM,P1*R
 14,UTM,-40B
 14,VZM,*NS1.BLANK
 14,UTM,-21B
 14,V1M,SLMIST.NOT 1
 *NS2:11,V1M,*NS3
 13,VJM,P1*R
 14,UTM,-40B
 14,VZM,*NS2.BLANK
 14,UTM,-20B
 14,V1M,SLMIST.NOT 0
 1,UTM,1.S=S+1
 ,UJ,*NS2
 *NS3:,ITA,1
 ,ATX,STEP
 ,ASN,64+3
 ,U1A,SLMIST.S>7
 ,UJ,SLCARD
C------OБPAБOTKA *LL
 *LL:,BSS,
 ,XTA,RABOTA1
 13,VJM,ENDLI.OK-E LIST
 14,VTM,4
 *LL0:12,VTM,14B
 *LL1:13,VTM,SLCARD
 ,UJ,RBT0.ПP.PAБOTЫ
C------OБPAБOTKA *HL
 *HL:,BSS,
 ,XTA,RABOTA1
 13,VJM,ENDLI
 14,VTM,10B
 ,UJ,*LL0
C------OБPAБOTKA *LO
 *LO:,BSS,
 ,XTA,RABOTA1
 13,VJM,ENDLI
 12,VTM,14B
 *LO1:13,VTM,SLCARD
 ,UJ,RBT
C------OБPAБOTKA *LP
 *LP:,BSS,
 ,XTA,RABOTA1
 13,VJM,ENDPU.OK-E PUNCH
 ,CALL,COLPUNB*
 14,VTM,20B
 *LP0:12,VTM,60B
 ,UJ,*LL1
C________ OБPAБOTKA *LT
 *LT:,XTA,RABOTA1.
 13,VJM,ENDLI.OKOHЧ. ПPEДЫДYЩ LIST
 14,VTM,4.
 ,XTA,=1.
 ,ATX,LTPR.ПPИЗHAK *LT
 ,UJ,*LL0
C------OБPAБOTKA *CP
 *CP:,BSS,
 9,VTM,3
 13,VJM,PARAMT .ИДEHT. B TEXT(RR1)
 14,V1M,SLMIST
 ,XTA,RABOTA1
 13,VJM,ENDPU
 ,XTS,RR1
 ,CALL,FORMKOZA.PUNCH NAME
 14,VTM,40B
 ,UJ,*LP0
C------OБPAБOTKA *PO
 *PO:,BSS,
 ,XTA,RABOTA1
 13,VJM,ENDPU
 12,VTM,60B
 ,UJ,*LO1
C------OБPAБOTKA *U
    *U:,BSS,
 9,VTM,3
 13,VJM,PARAMT.ИДEHT. B TEXT(RR1)
 14,V1M,SLMIST
 14,VTM,-13.
 *U1:14,UTC,CARD.
 ,XTA,13.
 14,UTC,*TIKTAK*
 ,ATX,10+13.*U:<ИMЯ>.......
 14,VLM,*U1.  *U: ....
 ,XTA,RR1
 ,ATX,NOWRIT
 14,VTM,100B
 12,VTM,100B
 ,UJ,*LL1
C------OБPAБOTKA *UO
 *UO:,BSS,
 ,XTA,RABOTA1.
 ,AAX,=100
 ,UZA,SLCARD.HE *USER
 ,XTA,=I1
 ,UTC,*TIKTAK*
 ,ATX,24.
 ,XTA,=:4
 ,ATX,CREAD.
 14,VTM,READ OLD
 ,ITA,14.
 13,VJM,NAMCALL2.
 ,XTA,RABOTA1
 13,VJM,ENDUZ
 12,VTM,100B
 ,UJ,*LO1
C---   OKOHЧAHИE PAБOTЫ ПO *U
 ENDUZ:,BSS,
 ,AAX,=100
 13,UZA,
 ,XTA,
 ,ATX,NOWRIT
 ,UTC,*TIKTAK*
 ,ATX,24.
 ,WTC,17B.OБECПEЧEHИE
 ,ATX,.ПEPEЗAГPYЗKИ
 14,VTM,XREAD/
 14,XTA,
 ,AOX,=4000 0000 0000
 ,AEX,=4000 0000 0000
 14,ATX,.NONCOSY
 ,UJ,COSY0
C---   OKOHЧAHИE PAБOTЫ *LP,*CP
 ENDPU:,BSS,
 ,ATX,ID2
 ,ITA,13
 ,XTS,ID2
 ,AAX,=20
 ,UZA,EPU.HE *LP
 ,CALL,COLPUNE*
 EPU:,XTA,ID2
 ,AAX,=40
 ,UZA,EPU1.HE *CP
 ,XTS,
 13,VJM,FORMKOZA+1
 EPU1:,STX,
 ,ATI,13
 13,UJ,
C---   OKOHЧAHИE PAБOTЫ *LL,*HL
 ENDLI:,BSS,
 ,AAX,=14
 13,UZA,
 ,XTA,
 ,ATX,NAHL
 ,ATX,LTPR.
 13,UJ,
C------OБPAБOTKA *B
 *B:,BSS,
 13,VJM,PARA.N1,N2
 13,VJM,PROVE.KOHTPOЛЬ
 13,VJM,TONNN.N-HEYПP
 ,XTA,N2
 ,U1A,SLMIST.ECTЬ N2
C      OБPAЩEHИE K POISK
 ,ATX,ZAP
 ,XTA,=1
 ,ATX,PRBEF
 14,VTM,N1
 ,ITA,14
 13,VJM,POISK
 ,UJ,SLCARD
C------OБPAБOTKA *I
 *I:,BSS,
 13,VJM,PARA
 13,VJM,PROVE
 13,VJM,TONNN
 ,XTA,N2
 ,U1A,SLMIST.ECTЬ N2
 ,ATX,ZAP
 ,ATX,PRBEF
 14,VTM,N1
 ,ITA,14
 13,VTM,SLCARD
 ,UJ,POISK
C------OБPAБOTKA *D
 *D:,BSS,
 13,VJM,PARA
 13,VJM,PROVE
 13,VJM,TONNN
 ,XTA,
 ,ATX,ZAP
 ,XTA,=1
C      ПOДBOД K N1
 ,ATX,PRBEF
C       ИCKЛЮЧEHИE N1 ИЛИ N1-N2
 14,VTM,N1
 ,ITA,14
 13,VJM,POISK
 14,VTM,N1
 ,XTA,N2
 ,UZA,*DEL
 14,VTM,N2
C      N2 B ЭTAЛOH
 ,XTA,N2
 ,ATX,NETAL
 ,XTA,N2+1
 ,ATX,NETAL+1
 *DEL:,XTA,
 ,ATX,PRBEF
 *DEL1: ,XTA,=1
 ,ATX,ZAP
 13,VJM,PROVE
 ,ITA,14
 13,VJM,POISK
 ,UJ,SLCARD
C-----OБPAБOTKA *DA
 *DA:,BSS,.
 13,VJM,PARA
 13,VJM,TONNN
 ,XTA,N2
 ,U1A,SLMIST.ECTЬ N2
 14,VTM,N1
 ,UJ,*DEL
C------OБPAБOTKA *DB
 *DB:,BSS,.
 13,VJM,PARA
 13,VJM,TONNN
 ,XTA,N2
 ,U1A,SLMIST.ECTЬ N2
 14,VTM,N1
 ,XTA,=1
 ,ATX,PRBEF
 ,UJ,*DEL1
C------OБPAБOTKA *L
 *L:,BSS,
 13,VJM,TONNN
 11,VZM,*LIN.ECTЬ :
 ,UJ,SLCARD
C      OФOPMЛEHИE KAPTЫ
 *LIN:14,VTM,-11
 ,XTA,=H
 :14,ATX,CARD/+11
 14,VLM,*LIN+1.ПPOБEЛЫ
 8,VTM,XID
 9,VTM,CARD/
 10,VTM,12
 13,VJM,DO*LENT.ЛEHTA
C      ПEPEHOC TEKCTA C *L:TEXT
 *LIN1:8,VTM,XCARD
 13,VJM,P1*R
 11,V1M,*LIN2
 8,VTM,XID
 13,VJM,W*P1
 ,UJ,*LIN1
 *LIN2:,BSS,
 ,XTA,PROGON.
 ,UIA,SLCARD.
 14,VTM,CARD/
 ,ITA,14
 13,VTM,SLCARD
 ,UJ,NAMCALL2.CEPBИC
C------OБPAБOTKA *T:N1,N2
 *T:,BSS,
 13,VJM,PARA
 13,VJM,DOKTAKE
 ,XTA,N2
 ,UZA,SLMIST.HET N2
 ,XTA,=1
 ,ATX,YESTAKE
 ,ATX,NOENDR
 ,ATX,ZAP
 ,ATX,PRBEF
 ,XTA,BLTAKE
 ,AOX,TOUT
 ,UZA,*T1.HET *BT,*TO
 ,XTA,
 ,ATX,ZAP
 *T1:,BSS,
 14,VTM,N1
 ,ITA,14
 13,VJM,POISK.K N1
C      XAPAKTEPИCTИKA TAKE
 14,VTM,NTAK
 ,ITA,14
 ,ATX,XNOENDTA
 ,ATX,NOENDTA
C      ПEPEHOC N2 B NTAK
 ,XTA,N2
 ,ATX,NTAK
 ,XTA,N2+1
 ,ATX,NTAK+1
 ,UJ,SLCARD
C------OБPAБOTKA *TN:NAME
 *TN:,BSS,
 13,VJM,DOKTAKE
 9,VTM,3
 13,VJM,PARAMT.ИДEHTИФИKATOP B ISO
 14,V1M,SLMIST.ПAPAMETP C OШИБKOЙ
 14,VTM,*MONCRD*
 14,XTA,
 ,ATX,TN
 14,XTA,1
 ,ATX,TN+1
 ,XTA,=1
 ,ATX,YESTAKE
 ,ATX,NOENDR
 ,ATX,ZAP
 ,ATX,PRBEF
 ,XTA,BLTAKE
 ,AOX,TOUT
 ,UZA,*TN3.HET *BT,*TO
 ,XTA,
 ,ATX,ZAP
C      ПOДBOД K KAPTE NAME(BEFORE)
 *TN3:14,VTM,TN
 ,ITA,14
 ,AEX,=:3
 13,VJM,POISK
 ,XTA,=:4
 ,ATX,XNOENDTA.X-KA END
 ,ATX,NOENDTA
 ,UJ,SLCARD
C------OБPAБOTKA *TO
 *TO:,BSS,
 13,VJM,DOKTAKE
 ,XTA,=1
 ,ATX,TOUT
 ,UJ,SLCARD
C------OБPAБOTKA *NO
 *NO:,BSS,
 ,XTA,=H
 ,ATX,NUMBERI
 ,XTA,NUMBERE
 ,ATX,NUMBERI+1
 12,VTM,3
 13,VJM,RBT
 ,UJ,SLCARD
C------OБPAБOTKA *BT
 *BT:,BSS,
 13,VJM,DOKTAKE
 ,XTA,=1
 ,ATX,BLTAKE
 ,UJ,SLCARD
C------OБPAБOTKA *I.OБPAЗ
 *I*:,BSS,
 13,VJM,LENT0.X-KA
 13,VJM,   PROVE
 13,VJM,TONNN
 ,XTA,
 *I*1:,ATX,ZAP
 *I*2:,ATX,PRBEF
 ,XTA,RR1
 *I*4:13,VTM,SLCARD
 ,UJ,POISK
C------OБPAБOTKA *B.
 *B*:,BSS,
 13,VJM,LENT0
 13,VJM,PROVE
 13,VJM,TONNN
 ,XTA,
 ,ATX,ZAP
 ,XTA,=1
 ,UJ,*I*2
C      OБPAБOTKA *IF.
 *IF*:,BSS,
 13,VTM,*RO
 ,UJ,PROVE
C------OБPAБOTKA *D.
 *D*:,BSS,
 13,VJM,LENT0
 13,VJM,PROVE
 13,VJM,TONNN
 ,XTA,
 ,ATX,ZAP
 ,XTA,=1
 ,ATX,PRBEF
 ,XTA,RR1
 13,VJM,POISK.ПOДBOД
 ,XTA,=1
 ,ATX,ZAP
 13,VJM,PROVE
C      ИCKЛЮЧEHИE KAPTЫ
 ,XTA,
 ,ATX,PRBEF
 ,ATX,YESCARD
 ,UJ,SLCARD
C------OБPAБOTKA *DB.
 *DB*:,BSS,
 13,VJM,LENT0
 13,VJM,PROVE
 13,VJM,TONNN
 ,XTA,=1
 ,UJ,*I*1
C------OБPAБOTKA *DA.
 *DA*:,BSS,
 13,VJM,LENT0
 13,VJM,PROVE
 13,VJM,TONNN
 ,XTA,=1
 ,ATX,ZAP
 ,XTA,
 ,UJ,*I*2
C------OЮPAБOTKA *IQ.
 *IQ*:,BSS,
 13,VJM,YKAZQ.X-KA
 13,VJM,PROVE
 13,VJM,TONNN
 ,XTA,
 ,ATX,ZAP
 ,XTA,
 ,UJ,*I*2
C------OБPAБOTKA *DQ.
 *DQ*:,BSS,
 13,VJM,YKAZQ
 13,VJM,PROVE
 13,VJM,TONNN
 ,XTA,=1
 ,ATX,ZAP
 ,UJ,*DQ*-1
C      OБPAБOTKA *DF.
 *DF*:,BSS,
 ,XTA,NOENDTA
 ,UIA,SLMIST.
 ,ATX,NOENDR
 13,VTM,*REO
 ,UJ,PROVE
C------OБPAБOTKA *C:,*C.,*C/
 *C:,BSS,
 *C*:,BSS,
 *C/:,BSS,
 8,VTM,XID
 ,XTA,=H
 ,ATX,IDID.HAЧAЛO
 ,ATX,IDID+1.COMMENT
 9,VTM,IDID+2
 10,VTM,10
 13,VJM,DO*LENT.ЛEHTA
C      ПEPEHOC KOMMEHTAPИЯ
 *C1:8,VTM,XCARD
 13,VJM,P1
 11,VIM,*C2
 13,VJM,RSIM
 8,VTM,XID
 13,VJM,W*P1
 11,VIM,*C3
 ,UJ,*C1
C      ДOПOЛHEHИE ПPOБEЛAMИ
 *C2:14,VTM,40B
 8,VTM,XID
 13,VJM,W*P1
 11,VZM,*C2
C      ПEЧATЬ 3-X CTPOK:
C      CARCOM,COMMENT,CARCOM
 *C3:,BSS,
 8,VTM,CARCOM
 13,VJM,PERCARD
 13,VJM,PRINTCD.CARCOM
 8,VTM,IDID
 13,VJM,PERCARD
 13,VJM,PRINTCD.COMMENT
 8,VTM,CARCOM
 13,VJM,PERCARD
 13,VTM,SLCARDN
 ,UJ,PRINTCD.CARCOM
C---   ПEPEHOC CTPOKИ B CARD
C      I8=AДPEC CTPOKИ
 PERCARD:,BSS,
 9,VTM,-13
 PCA:9,UTC,
 8,XTA,13
 9,ATX,CARD+13
 9,VLM,PCA
 13,UJ,
C------OБPAБOTKA *D/
 *D/:,BSS,
 ,XTA,=1
 ,ATX,DVDV
 13,VJM,TONNN.NNN
 13,VJM,LONG/.ДЛИHA,X-KA
 ,XTA,
 ,ATX,ZAP
 ,XTA,=1
 ,ATX,PRBEF
 ,XTA,NNN
 ,UIA,*DK/.NNN NEQ 0
 ,XTA,RRY
 15,ATX,
 13,VJM,POISK/.ПOДBOД
 ,XTA,DVDV
 ,ATX,ZAP
 ,XTA,
 ,STX,PRBEF
 *D/A:13,VJM,POISK/.DELETE
 ,XTA,RR1*
 ,UZA,SLCARD
 ,XTA,
 ,ATX,RR1*
 8,VTM,XID
 9,VTM,XCARD
 13,VTM,*L/+1
 ,UJ,L1*TO*L2
 *DK/:,XTA,DVDV
 ,ATX,ZAP
 ,ATX,PRBEF
 ,XTA,NNN
 13,VJM,POISK/.DEL Q
 ,UJ,*L/0.INSERT
C------OБPAБOTKA *V/
 *V/:,XTA,=:1
 ,ATX,DVDV
 ,UJ,*D/+1
C------OБPAБOTKA *DB/
 *DB/:,BSS,
 ,XTA,=1
 ,ATX,DVDV
 13,VJM,LONG/
 ,XTA,DVDV
 ,ATX,ZAP
 *DB/0:,ATX,PRBEF
 ,XTA,RRY
 ,UJ,*D/A
C------OБPAБOTKA *VB/
 *VB/:,XTA,=:1
 ,ATX,DVDV
 ,UJ,*DB/+1
C------OБPAБOTKA *DA/
 *DA/:,BSS,
 ,XTA,=1
 ,ATX,DVDV
 13,VJM,LONG/
 ,XTA,DVDV
 ,ATX,ZAP
 ,XTA,
 ,UJ,*DB/0
C------OБPAБOTKA *VA/
 *VA/:,XTA,=:1
 ,ATX,DVDV
 ,UJ,*DA/+1
C------OБPAБOTKA *DF/
 *DF/:,BSS,
 13,VJM,LONG/
 ,XTA,=1
 ,ATX,Q72
 ,ATX,WBLAN
 13,VJM,VSTAV/
  ,UJ,SLCARD
C------OБPAБOTKA *I/
 *I/:,BSS,
 13,VJM,TONNN
 13,VJM,LONG/
 ,XTA,
 ,ATX,ZAP
 ,XTA,NNN
 ,UIA,*DK/+1
 *I/1A:13,VTM,SLCARD
 ,ATX,PRBEF
 ,XTA,RRY
 ,UJ,POISK/
C------OБPAБOTKA *B/
 *B/:,BSS,
 13,VJM,LONG/
 ,XTA,
 ,ATX,ZAP
 ,XTA,=1
 ,UJ,*I/1A
C------OБPAБOTKA *L/
 *L/:,BSS,
 13,VJM,TONNN
 13,VJM,LONG/
 ,XTA,NNN
 ,UZA,*L/0
 ,ATX,LONG
 *L/0:13,VTM,SLCARD
 ,UJ,VSTAV/
C------OБPAБOTKA *IF/
 *IF/:,BSS,
 13,VJM,LONG/
 13,VTM,*L/0
 ,UJ,POISKF/
C------OБPAБOTKA *M/
 *M/:,BSS,
 13,VJM,LONG/
 13,VJM,POISKF/
 13,VJM,VSTAV/
 13,VJM,PRINTCD
 13,VTM,SLCARDN
 ,UJ,DOKR/
C------OБPAБOTKA *SS
 *SS:,BSS,
 8,VTM,XCARD
 *SS0:11,V1M,*SS2
 13,VJM,P1*R
 14,UTM,-40B
 14,VZM,*SS0
 *SS1:14,UTM,40B
 ,ITA,14
 ,ATX,FOR*
 ,UJ,SLCARD
 *SS2:14,VTM,12B.*-40B
 ,UJ,*SS1
C------OБPAБOTKA *Z
 *Z:,BSS,
 ,XTA,WWW/
 ,UIA,*Z1.HET KAPTЫ
C      ПPOДBИЖEHИE ЗA OДHY KAPTY
 ,XTA,=:1
 ,ATX,PRBEF
 13,VJM,POISK
 ,XTA,
 ,ATX,ZAP
 ,ATX,PRBEF
C      ПOДBOД K CЛEДYЮЩEИ
 ,XTA,=:1
 13,VJM,POISK
 13,VTM,SLCARD
 ,UJ,PROVE
 *Z1:,XTA,
 ,ATX,WWW/
 ,UJ,SLCARD
C------OБPAБOTKA *Y
 *Y:,BSS,
 ,XTA,
 ,ATX,BLYPR
 ,XTA,=777777
 ,ATX,YAR
 ,UJ,SLCARD
C------OБPAБOTKA *PAGE
 *YO:,BSS,
 ,XTA,.*PAGE
 ,ATX,YAR
 ,UJ,SLCARD
 *P:,BSS,
 13,VJM,PRINTCD
 13,VTM,SLCARDN
 ,XTA,NOLISFL*
 13,U1A,.NO LIST
 ,XTA,=2.
 ,ATX,PFLAG.
 ,UJ,PRINT8//+2
C------OБPAБOTKA *PP
 *PP:,BSS,
 13,VJM,PRINTCD
 13,VTM,SLCARDN
 ,XTA,NOLISFL*
 13,U1A,
 ,XTA,=3.
 ,ATX,PFLAG.
 ,UJ,PRINT8//+3
C------OБPAБOTKA *(((
C      (YЧET YPOBHEЙ BЛ-TИ)
 *3/1:,BSS,
 ,XTA,BLYPR
 ,UZA,*3A.HET БЛ-KИ
 ,AAX,=:1
 ,UZA,SLMIST.HE БЫЛO *(((
 *3A:,BSS,
 ,XTA,YROV
 ,UIA,*3D.NOT 0
 13,VJM,DOKR/.OK-E /
 ,XTA,=:1
 ,ATX,BLYPR
 ,XTA,=1
 ,ATX,YROV
 ,UJ,SLCARD
 *3D:,XTA,YROV
 ,ARX,=1
 ,ATX,YROV.YROV+1
 ,UJ,NOYPR
C------ПEЧATЬ ДATЫ И ЗAГOЛOBKA
 EDNAM:,XTA,NOLISFL*
 13,U1A,
 ,ITA,13
 ,XTS,
 ,CALL,DATAPR*.DATA
 14,VTM,TEXTA
 ,ITS,14
 14,VTM,TEXTB-1
 ,ITS,14
 ,XTS,=1
 ,CALL,PRINT8//.HAЧAЛO
 ,STX,
 ,ATI,13
 14,VTM,TEXTB
 ,ITS,14
 14,VTM,TEXTB+1
 ,ITS,14
 ,XTS,=4
 ,UJ,PRINT8//.KOHEЦ
C------TAБЛИЦЫ ДЛЯ AHAЛИЗA
C      YПPABЛЯЮЩИX CЛOB:
C      ЛEBAЯ KOMAHДA:48-42 J,15-1-S
C       J-KOД HAЧAЛЬHOГO CИMBOЛA
C       S=1-ECTЬ CCЫЛKA HA ДOП.TAБЛИЦY
C      ПPABAЯ KOMAHДA:48-N,15-1-A
C       N=1-PAЗPEШEHO YKAЗAHИE ЧИCЛA
C       A(S=0)-AДPEC ПP.OБPAБOTKИ
C       A(S=1)-AДPEC ДOП.TAБЛИЦЫ
C          KOHEЦ:HYЛEBOE CЛOBO
CX     ДOПOЛHИTEЛЬHЫE TAБЛИЦЫ:
C      ИMEЮT ПOДOБHYЮ CTPYKTYPY:S=0
C      KOHEЦ:ЛEBAЯ KOMAHДA=0
C*
C**    TAБЛИЦA PEДAKTИPOBAHИЯ :
C*
 TWOT:,BSS,
 10,Z10,1.R
 ,UJ,READ
 10,Z34,1.W
 ,UJ,WRITE11
 9,Z30,1.N
 ,UJ,NUMBER1
 8,Z10,1.B
 ,UJ,BBB1
 9,Z04,.I
 8,UJ,*I
 8,Z20,1.D
 ,UJ,*DELET
 9,Z20,1.L
 ,UJ,LINE
 10,Z20,1.T
 ,UJ,TAKE1
 11,Z10,.Z
 ,UJ,*Z
 8,Z24,1.E
 ,UJ,ENDE1
 10,Z00,1.P
 ,UJ,PUNCH
 8,Z14,1.C
 ,UJ,COMME
 11,Z04,1.Y
 ,UJ,YARD
 9,Z00,1.H
 ,UJ,HEAD
 10,Z24,1.U
 ,UJ,UZER
 10,Z14,.S
 ,UJ,*SS
 8,Z04,.A
 ,UJ,*A.
 ,OCT,
C---   ДOП.TAБЛИЦA ДЛЯ *D
 *DELET:8,Z04,.A
 8,UJ,*DA
 8,Z10,.B
 8,UJ,*DB
 ,Z00,
 8,UJ,*D
C---   ДOП.TAБЛИЦA ДЛЯ *E
 ENDE1:8,Z24,.E
 ,UJ,*EE
 ,OCT,
C---   ДOП.TAБЛИЦA ДЛЯ *W
 WRITE11:8,Z14,.C
 ,UJ,*WC
 9,Z34,.O
 ,UJ,*WO
 ,Z00,
 ,UJ,*W
C---   ДOП.TAБЛИЦA ДЛЯ *N
 NUMBER1:9,Z04,.I
 ,UJ,*NI
 9,Z34,.O
 ,UJ,*NO
 10,Z14,.S
 ,UJ,*NS
 ,Z00,
 ,UJ,*N
C---   ДOП.TAБЛИЦA ДЛЯ *B
 BBB1:,BSS,
 10,Z20,.T
 ,UJ,*BT
 ,Z00,
 8,UJ,*B
C---   ДOП.TAБЛИЦA ДЛЯ *T
 TAKE1:9,Z34,.O
 ,UJ,*TO
 9,Z30,.N
 ,UJ,*TN
 ,Z00,
 ,UJ,*T
C---   ДOП.TAБЛИЦA ДЛЯ *R
 READ:9,Z34,.O
 ,UJ,*RO
 ,Z00,
 ,UJ,*R
C---   ДOП.TAБЛИЦA ДЛЯ *L
 LINE:9,Z34,.O
 ,UJ,*LO
 9,Z20,.L
 ,UJ,*LL
 10,Z00,.P
 ,UJ,*LP
 10,Z20,.T
 ,UJ,*LT.
 ,Z00,
 8,UJ,*L
C---   ДOП.TAБЛИЦA ДЛЯ *P
 PUNCH:9,Z34,.O
 ,UJ,*PO
 10,Z00,.P
 ,UJ,*PP
 ,Z00,
 ,UJ,*P
C---   ДOП.TAБЛИЦA ДЛЯ *C
 COMME:10,Z00,.P
 ,UJ,*CP
 ,Z00,
 ,UJ,*C
C---   ДOП.TAБЛИЦA ДЛЯ *Y
 YARD:9,Z34,.O
 ,UJ,*YO
 ,Z00,
 ,UJ,*Y
C---   ДOП.TAБЛИЦA ДЛЯ *H
 HEAD:9,Z20,.L
 ,UJ,*HL
 ,Z00,
 ,UJ,SLMIST
C---   ДOП.TAБЛИЦA ДЛЯ *U
 UZER:9,Z34,.O
 ,UJ,*UO
 ,Z00,
 ,UJ,*U
C------TAБЛИЦA PEДAKTИPOBAHИЯ .
 ONET:8,Z20,1.D
 ,UJ,*D*1
 9,Z04,1.I
 ,UJ,*I*9
 8,Z10,.B
 8,UJ,*B*
 9,Z20,.L
 8,UJ,*L
 8,Z14,.C
 ,UJ,*C*
 ,OCT,
C------TAБЛИЦA PEДAKTИPOBAHИЯ /
 ONE/:8,Z20,1.D
 8,UJ,*D/1
 9,Z04,1.I
 8,UJ,*I/1
 10,Z30,1.V
 ,UJ,*V/1
 9,Z20,.L
 8,UJ,*L/
 8,Z10,.B
 ,UJ,*B/
 9,Z24,.M
 ,UJ,*M/
 8,Z14,.C
 ,UJ,*C/
 ,OCT,
C------ДOП.TAБЛИЦЫ PEД-Я .
C---   ДOП.TAБЛИЦA ДЛЯ *D
 *D*1:8,Z04,.A
 8,UJ,*DA*
 8,Z10,.B
 8,UJ,*DB*
 10,Z04,.Q
 8,UJ,*DQ*
 8,Z30,.F
 8,UJ,*DF*
 ,Z00,
 8,UJ,*D*
C---   ДOП.TAБЛИЦA ДЛЯ *I
 *I*9:10,Z04,.Q
 8,UJ,*IQ*
 8,Z30,.F
 8,UJ,*IF*
 ,Z00,
 8,UJ,*I*
C------ДOП.TAБЛИЦЫ PEД-Я /
C---   ДOП.TAБЛИЦA ДЛЯ *D
 *D/1:8,Z30,.F
 ,UJ,*DF/
 8,Z04,.A
 ,UJ,*DA/
 8,Z10,.B
 ,UJ,*DB/
 ,Z00,
 8,UJ,*D/
C---   ДOП.TAБЛИЦA ДЛЯ*V
 *V/1:8,Z30,.F
 ,UJ,*DF/
 8,Z04,.A
 ,UJ,*VA/
 8,Z10,.B
 ,UJ,*VB/
 ,Z00,
 ,UJ,*V/
C---   ДOП.TAБЛИЦA ДЛЯ *I
 * I/1:8,Z30,.F
 ,UJ,*IF/
 ,Z00,
 8,UJ,*I/
C      AДPEC OЧEPEДHOЙ KAPTЫ ЗAДAHИЯ
 ACARD:,,
 ,Z00,CARD
C      TAБЛИЦA YПP.KAPT CTAPOГO PEДAKTOPA
 CARCOM:,ISO,72H
 ,CONT,12H   *
 OLDED:0,ISO,18H*INSERT
 1,ISO,18H*INSERT BELOW
 2,ISO,18H*DELETE
 3,ISO,18H*TAKE
 4,ISO,18H*LAST TAKE
 ,OCT,.KOHEЦ
 TEXTA:,ISO,16HPEДAKTOP TEKCTOB
 TEXTB:,ISO,12H(01.06.82)
 ,BSS,
C      PEЗYЛЬTAT PEДAKTИPOBAHИЯ /
 CARD/:,BSS,12
 ,ISO,11H
 ,CONT,1H'12'
 ,BSS,
C      CПPABKA ФAЙЛA ЧTEHИЯ
 DOKYR:0,ISO,6H ФAЙЛ
 1,OCT,
 ,ISO,6H''''''
 2,ISO,12H:CЧИTAHO  ''''
 4,OCT,
 5,ISO,6H KAPT
 6,ISO,6H(''''''''''
 7,OCT,
 8,ISO,6H ЗOH)
 DOKYZ:,ISO,6H ЗOH)
 DOKYT:,ISO,6H TP.)
 ERTEXT:0,ISO,20HHEKOPPEKTHAЯ YПPABЛЯ
 ,CONT,14HЮЩAЯ KAPTA
 ,OCT,
  1,ISO,20HФAЙЛ ЗAПИCИ И ФAЙЛ
 ,CONT,16HЧTEHИЯ COBПAДAЮT
 ,OCT,
 2,ISO,12HПYCTOЙ OБPAЗ
 ,OCT,
 3,ISO,18HБЫЛA *DELETE FINAL/
 ,OCT,
  4,ISO,22HHEПOHЯTHO ЧTO ДEЛATЬ
 ,CONT,12H(*W,*LL,...)
 ,OCT,
 5,ISO,24HHET ИCKOMOГO OБPAЗA
 ,OCT,
  6,ISO,20HB ФAЙЛE ЧTEHИЯ HET
 ,CONT,14HИCKOMOЙ KAPTЫ
 ,OCT,
  7,ISO,22HHE YKAЗAH ФAЙЛ ЧTEHИЯ
 ,OCT,
 8,ISO,19HБOЛЬШE 72 CИMBOЛOB
 ,CONT,23HИЛИ HET ИCKOMOГO OБPAЗA
 ,OCT,
 9,ISO,24HПEPBЫЙ CИMBOЛ HE ПPOБEЛ
 ,OCT,
 10,ISO,24HБЫЛИ OБHAPYЖEHЫ OШИБKИ
 ,OCT,
 ,ISO,18HMAЛO MЛ(MБ) ИЛИ
 11,ISO,24HФAЙЛ ЗAПИCИ БOЛЬШE 2MБ
 ,OCT,
 12,ISO,30HПPOЧИTAHA HETEKCTOBAЯ KAPTA
 ,OCT,
 13,ISO,48HPEДAKTOP HE ПOHИMAET CT. MACCИBOB И BINARY'41'
 ,OCT,.
 TOTCEE:,ISO,24HHET KOHЦA ЗAДAHИЯ (*EE)'41'
 ,OCT,.
 15,ISO,42HHEПPABИЛЬHO ЗAДAHЫ ПAPAMETPЫ PEДAKTOPA
 ,OCT,
 DOKYW:0,ISO,6H ФAЙЛ
C      CПPABKA ФAЙЛA ЗAПИCИ
 1,OCT,
 ,ISO,6H''''''
 2,ISO,12H:ЗAПИCAHO ''''
 4,OCT,
 5,ISO,6H KAPT
 6,ISO,6H(''''''''''
 7,OCT,
 8,ISO,6H ЗOH)
 READ OLD:,ISO,83H*READ OLD
 ,CONT,1H'12'
 END FILE:,ISO,12H*END FILE '311''012'
 NUMBERE:,ISO,6H     '012'
 IDID:,BSS,12
 ,ISO,12H   **
 ,BSS,
C      NEQ=0-БЛOKИPOBKA YПPABЛЯЮЩИX
 BLYPR:,BSS,1
C      OЧEPEДHAЯ KAPTA ЗAДAHИЯ
 CARD:,BSS,14
C      X-KA KAPTЫ ЗAДAHИЯ
 XCARD:,BSS,4
 FNNN:,BSS,1.(YKAZQ)
C      K-BO CЛEД.HEYПP.KAPT
 NNN:,BSS,1
C      AДP.OБPAБOTKИ YПP.KAPTЫ
 XXX:,BSS,1
C      OЧEPEДHAЯ KAPTA ИЗ ФAЙЛA ЧTEHИЯ
 CARDR:,BSS,14
C      NEQ 0-KAPTA (ИЗ R) B ПAMЯTИ(BEFORE)
 YESCARD:,BSS,1
C      NEQ 0-ECTЬ HEЗAK.READ
 NOENDR:,BSS,1
C      AДPEC ФAЙЛA ЗAПИCИ
 AWRITE:,BSS,1
C      AДPEC ФAЙЛA ЧTEHИЯ
 AREAD:,BSS,1
C      AДPEC U-ПOДПPOГPAMMЫ
 NOWRIT:,BSS,1
 ZAKAZP:,BSS,1.KOПИЯ *REGIM//
C     HAЧI COCTOЯHИE MAГAЗИHA
 MAG:,BSS,1
C     ЗAKAЗ BOЗBPATA ПPИ OШИБKAX
 ZAKAZ:,BSS,1
C     CPCTOЯHИE 20B
 SJUMP:,BSS,1
C       ШAГ HYMEPAЦИИ(S-1)
 STEP:,BSS,1
C      KOД YПPABЛЯЮЩEГO CИMBOЛA
 FOR*:,BSS,1
C      NEQ 0-ПPOГOH ДO *EE
 PROGON:,BSS,1
C      =1 *D,=:1 *V
 DVDV:,BSS,1
C       ПP. ЗAMEHЫ ПPИ *V/
 RR1*:,BSS,1
C      OЧEPEДHOЙ HOMEP(PEД-Я :)
 NUMBERI:,BSS,2
C      ЭTAЛOHHЫЙ HOMEP
 NETAL:,BSS,2
C      NEQ 0-БЛ-KA *R=*W
 BL*R*W:,BSS,1
C      NEQ 0-БЛOK-KA AREAD=CREAD
 BLARECRE:,BSS,1
 RR1:,BSS,1
 RR2:,BSS,1
 RRG:,BSS,1
C      ПEPBЫЙ HOMEP HA YПP.KAPTE
 N1:,BSS,2
C      BTOPOЙ HOMEP HA YПP.KAPTE
 N2:,BSS,2
C      NEQ 0-ЗAПИCЬ ЗAПPEЩEHA(DELETE)
 ZAP:,BSS,1
 XID1:,BSS,4
C      OГPAHИЧEHИE ФAЙЛA ЗAПИCИ
 AOGR:,BSS,1
C      ШKAЛA CEPBИCA
C      1P.=1-*N,2-*NI,3-*LL,
C      4-*HL,5-*LP,6-*CP,7-*U
 RABOTA1:,BSS,1
C      KOПИЯ ШKAЛЫ RABOTA1
 RABOTA2:,BSS,2
C      NEQ 0-БЫЛA *BT
 BLTAKE:,BSS,1
C      NEQ 0-БЫЛA *TO
 TOUT:,BSS,1
C      NEQ 0-БЫЛA *T,*TN
 YESTAKE:,BSS,1
C      NEQ 0-HEЗAKOHЧEH TAKE
 NOENDTA:,BSS,1
C      ДЛЯ OPГAHИЗAЦИИ ПEЧATИ
 PRAB:,BSS,15
 SX1:,BSS,4
 SX2:,BSS,4
C      YO1-YO5-PAБ.BEЛИЧИHЫ YZOB
 YO1:,BSS,1
 YO2:,BSS,1
 YO3:,BSS,1
 YO4:,BSS,1
 YO5:,BSS,1
 LTPR:,BSS,1.
 LTPR1:,BSS,1.KOПИЯ LTPR
 PFLAG:,BSS,1.=2/3 - БЫЛA *P/*PP
C      NEQ 0-БЫЛA *Y
 YAR:,BSS,1
C      X-KA ИCXOДHOЙ KAPTЫ(PEД-E /)
 SX1/:,BSS,4
C      NEQ 0-KOHEЦ ИCXOДHOЙ KAPTЫ
 Q72:,BSS,1
C      X-KA PEЗYЛЬTATA PEД-Я /
 SXR/:,BSS,4
C      PEЗYЛЬTAT PEДAKTИPOBAHИЯ /
 SCARD:,BSS,14
C      NEQ 0-KOHEЦ PEЗYЛЬTATA PEД-Я /
 W72:,BSS,1
C      ДЛИHA ФPAГMEHTA(LONG/)
 LONG:,BSS,1
 X/CARD:,BSS,4
 LOBL:,BSS,1.(LONG/)
C      HAЧATO PEДAKTИPOBAHИE /
 WWW:,BSS,1
C      KOПИЯ WWW
 WWW/:,BSS,1
C      YPOBEHЬ CKOБOK *(((-*)))
 YROV:,BSS,1
C      БЫЛA *DF/
 WBLAN:,BSS,1
 YESCARD/:,BSS,1.(*WC)
 NOENDR/:,BSS,1.(*WC)
 ZAP/:,BSS,1.(*WC)
 CARDR/:,BSS,14.(*WC)
 AREAD/:,BSS,1.(*WC)
C      NEQ 0-ПP.'HE KAPTA PEЗYЛЬTATA'
 LLPRI:,BSS,1
C      =0-ИЩETCЯ NAME,NEQ-END
 NAEM:,BSS,1
 NAHL:,BSS,1
C      ИMЯ C KAPTЫ *TN
 TN:,BSS,2
C      ДЛЯ ПEЧATИ CПPABKИ READ
 FILE:,BSS,2
 QCAR:,BSS,1
 QZT:,BSS,1
 ZT:,BSS,2
C      ДЛЯ ПEЧATИ CПPABKИ WRITE
 FILE/:,BSS,2
 QCAR/:,BSS,1
 QZT/:,BSS,1
 ZT/:,BSS,2
C      ДЛЯ ПЧ.'ИMЯ-NЗOHЫ' (*HL)
 NAMESUB:,BSS,3
C      HYЖHA CПPABKA READ(AB.CИTYAЦИЯ)
 YRE:,BSS,1
 RRY:,BSS,1
C      X-KA HEЗAKOHЧEHHOГO TAKE
C      15-1-NTAK:*T:N1,N2
C      48=1:*TN
 XNOENDTA:,BSS,1
 PRBEF:,BSS,1
 NTAK:,BSS,2
 XID:,BSS,4
C      ПP.'KAPTA ИЗ READ'
C      48=1-KAPTA *READ OLD(*END FILE)
 CAREAD:,BSS,1
 PR111:,BSS,1
 XREAD//:,BSS,2
C      =0 ABOCT HE B MISTIKA2
 MYMY:,BSS,1
 ,END,
