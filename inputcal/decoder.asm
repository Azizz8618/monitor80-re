 DECODER:,NAME,.
C--------
 '
C******   ПOKAPTOЧHAЯ KOДИPOBKA
C         B COSY-ISO , CM , BINARY
 '
C......   HA BXOДE :
C         OБPAЗ KAPTЫ(24CЛ) B INC
 '
C......   HA BЫXOДE :
C      И14 = 0 - KOHEЦ ФAЙЛA
C          = 1 - ECTЬ PEЗ-T , ПK HE HAДO
C          = 2 - HET PEЗ-TA , ПK HAДO
C          = 3 - ECTЬ PEЗ-T , ПK HAДO
C         PEЗ.KAPTA(CTPOKA) B OUTC
 '
C......   PAЗHЫE MACCИBЫ-KAPTЫ
 *CARDS:LC,BLOCK,
 ,CONT,INC(24) .ИCXOДHAЯ
 ,CONT,REVC(20).ПEPEBEPHYTAЯ
 ,CONT,OUTC(25).PEЗYЛЬTAT
 ,CONT,UNPC(25).PACПAKOB.
C ,CONT,(15)    .HAKOПЛ.COSY
C......   ФЛAГИ KOДOB KAPT
 *CODES:LC,BLOCK,
 ,CONT,M COD
 ,CONT,FL SPC,FL COSY
 ,CONT,FL BIN
 ,CONT,FL XXX
 LEN1:,EQU,FL XXX-M COD.
C......   ФЛAГИ OШИБOK , CBЯЗAHHOE C HИMИ
 *ERRORS:LC,BLOCK,
 ,CONT,FL BLER
 ,CONT,FL ERR,FL COD
 ,CONT,*ER/TEX
 ,CONT,ER/KS
 ,CONT,*ER/BIN
 ,CONT,*ER/CM,*ER/COS
 LEN2:,EQU,*ER/COS-FLBLER.
C......   ФЛAГИ ПO ПOBOДY CM И COSY
 *SPCCMN:LC,BLOCK,
 ,CONT,FL MID
 ,CONT,ДAЙKA,HETPEЗ
C INF/BA:LC,BLOCK,(1)
C ,CONT,T/NAM.
 T/NAM:,SUBP,
 ERROFLG*:,LC,1
C......   ПOДПPOГPAMMЫ KOДИPOBOK
 ERRUPP*:,SUBP,.
 ERRCDC*:,SUBP,.
 ERRM20*:,SUBP,.
 ERRYПДK*:,SUBP,
 CM/COSY :,SUBP,.
 COSY*   :,SUBP,.
C......   ДPYГИE ПOДПPOГPAMMЫ
 CHECCOD*:,SUBP,.
 BEGCHEC*:,SUBP,.
 ERRSTA *:,SUBP,.
C......   ИCПOЛЬЗYEMЫE ИP :
 *1:,BLOCK,B,NC,R
C--------
 C:B,BSS,.
 ,UTC,SUM.
 ,ATX,.
 ,ITA,13.CПACEHИE
 ,UTC,I13.BOЗBPATA
 ,ATX,.
 13,VJM,REPLACE.ЗAMEHA PEГИCTPOB
 ,XTA,STM.БЫЛ BBOД
 ,UIA,W INP. - ДA
 ,XTA,FL SAV.ЗAПOM.KAPTA
 ,UZA,RET SW. - HET
 ,ATI,13.
C......   BOCCTAHOBЛEHИE KAPTЫ
 14,VTM,1-25.
 RESET:14,XTA,UNPC+24.
 14,ATX,OUTC+24.
 14,VLM,RESET.
 ,XTA,.
 ,ATX,FL SAV....
 13,UJ,.
 RET SW:,WTC,R SW.
 ,UJ,.
 W INP:14,VIM,HE ДK. - HE ДK
 R,VJM,CHEK STA.ПPOB.CM/COSY
 ,xta,FL XXX
 ,aex,PTAPE
 ,uza,END1
 14,VTM,END1.
 ,ITA,14.ДЛЯ BOЗBPATA
 ,ATX,R SW.ПOCЛE ERBIN
 ,XTA,FL BIN.
 ,UIA,ER BIN.
 END1:14,VTM,.ПPИЗHAK:KOHEЦ
 ,UJ,FUL RET.
 HE ДK:NC,UTM,1.
 ,CALL,CHECCOD*.
 ,XTA,M COD.
 ,WTC,M COD.
 ,UJ,M SWITCH.
 M SWITCH:,BSS,.
C......   ПEPEKЛЮЧATEЛЬ TИПOB KAPT
 R1:14,VTM,2.ПPOПYCK KAPTЫ
 ,UJ,FUL RET.
 :R,VTM,TEXT CRD.TEKCT.KAPTA
 ,UJ,CHEK STA.
 :,BSS,1.CTAHД.MACCИB
 STA 0:,XTA,FL BIN.
 ,UJ,TO STA. COSY
 :R,VTM,ERR CRD.BINARY ИЛИ
 ,UJ,CHEK STA.OШИБ.KAPTA
 TEXT CRD:,BSS,.
C......   ПPOBEPKA CПEЦ.KAPT
 LSTC:,EQU,ESTC-STC.
 14,VTM,-LSTC.
 ,XTA,OUTC.
 ,AAX,M1*5. ПPOБA :
 ,AEX,CNAM.KAPTA-*NAME
 ,UIA,TC 1. - HET
 ,XTA,FL NAM.
 ,UIA,END1.- BTOPOE HAME
 ,XTA,B1.
 ,ATX,FL NAM.
C......   YПAKOBKA 18 CИMBOЛOB NAME
 13,VTM,1-3.
C 12,VTM,UNPC+6.
 PAN 1:,BSS,
C PAN 1:11,VTM,1-6.
C PAN 2:,ASN,64-8.
C 12,AOX,.
C 12,UTM,1.
C 11,VLM,PAN 2.
 13,XTA,OUTC+3
 13,ATX,T/NAM+3.
 13,VLM,PAN 1.
C......
 TC 1:14,XTA,ESTC.
 ,AEX,OUTC.
 ,UZA,TC 2.- CПEЦ.KAPTA
 14,VLM,TC 1.
 ,UJ,CHEK BIN.- HE CПEЦ.KAPTA
 TC 2:,XTA,FL BIN.
 14,UJ,SWIT+LSTC.
 SWIT:,BSS,.
C......   ПEPEKЛЮЧATEЛЬ CПEЦ.KAPT
 :,UIA,BIN.*BINARY
 ,UJ,REVBIN.
 :,UZA,NO BIN.*END BINARY
 ,UJ,REV BIN.
 :,UZA,NO BIN.*END FILE
 ,UJ,END F.
 :,XTA,B1.*BLERR
 ,UJ,BL ERR.
 REV BIN:,AEX,B1.PEBEPC ПPИЗHAKA
 ,ATX,FL BIN. 'BINARY'
 ,UJ,R1.
 END F:R,VTM,READY . was PUTSTM.
 14,VJM,SAV CRD.CПACAEM KAPTY
 ER BIN:,BSS,.
C......   OTCYTCTBYET '*END BINARY'
 R,VJM,FORM NC.ЗA HOMEPOM ПK
 13,VTM,1-4.
 ,XTA,TNOB. 1-E CЛOBO
 ,ATX,OUTC.ДИAГHOCTИKИ
 ERB:13,XTA,TNOB+4.
 13,ATX,OUTC+5.ПEPEПИCЬ TEKCTA
 13,VLM,ERB....
 ,ATX,*ER/BIN.ФЛAГ 'OШ.BINARY'
 14,VTM,4.БЛOKИP.BBOДA
 ,UJ,FUL RET.HA BOЗBPAT
  PUTSTM:,BSS,
C  ,XTA,B1
C  ,UJ,ERC 2
 BL ERR:,BSS,.
C......   БЛOK. BЫБPOCA ПO OШИБKAM
 ,ATX,FL BLER.
C was ,UJ,R1.
 CHEK BIN:,BSS,.
C......   BЫДAЧA TEKCT.KAPT И BINARY
 ,XTA,FL BIN.
 ,UZA,NO BIN. - HE BINARY
 BIN:14,VTM,1-24.
 BIN1:14,XTA,INC+23. ПEPEПИCЬ
 14,ATX,OUTC+24.ИCX.KAPTЫ
 14,VLM,BIN1.
 ,XTA,BINCOD.
 ,ATX,OUTC.
 ,XTA,.
 ,ATX,FL ERR.
 READY:,BSS,.
 NO BIN:14,VTM,3.HOPM.PEЖИM
 ,UJ,FUL RET.HA BOЗBPAT
 TO STA:,BSS,.
 ,UIA,BIN.- KAPTA BINARY
C......   KAPTA CTAHД.MACCИBA/COSY
 ,XTA,B1.
 ,ATX,FL SPC.ПPИЗHAK:CM/COSY
 ,XTA,.
 ,ATX,FL ERR.
 ,CALL,CM/COSY.ПEPEBOД KAPTЫ
 ,XTA,FL ERR.OПPEДEЛEHИE
 R,VTM,STA 0..TOЧKИ BXOДA
 ,UIA,RET STA.ПPИ OШИБKE
 R,VTM,COSY.И БEЗ OHOЙ
 RET STA:,ITA,R.
 ,ATX,R SW.TOЧKA BXOДA
 STA 1:14,VTM,0.
 ,XTA,ДAЙKA.ФOPMИPOBAHИE
 ,ATX,STM.TИПA BOЗBPATA
 ,UZA,RS 1.И PEЖИMA
 14,UTM,2.CЛEД.BXOДA
 RS 1:,XTA,HETPEЗ.
 ,UIA,FUL RET.
 14,UTM,1.
 ,UJ,FUL RET....
 COSY:13,VTM,STA 1.CTPOKA COSY
 ,UJ,COSY*.C TOЙ ЖE KAPTЫ
 CHEK STA:,BSS,.
C......   ПPOBEPKA KOHЦA CM/COSY
 ,XTA,FL SPC.A CM/COSY
 R,UZA,. - И HE БЫЛO
 ,XTA,.
 ,ATX,FL SPC.
 ,XTA,FL MID.HAXOДИMCЯ
 ,AOX,ER/KS.BHYTPИ CM/COSY
 R,UZA,. - HET
 14,VJM,SAV CRD.CПACAEM KAPTY
 13,VTM,STA 1.
 ,UJ,ERR STA*.HA ДИAГHOCTИKY
 ERR CRD:,BSS,.
C......   KAPTA BINARY ИЛИ OШИБOЧHAЯ
 ,XTA,FL BIN.
 ,UIA,BIN.KAPTA BINARY
C......   HEПOHЯTAЯ KAPTA
 ,ATX,STM.KAPTA HE HYЖHA
 14,VTM,LERC.
 ,ITA,14.BOЗBPAT ПOCЛE
 ,ATX,R SW.ДИAГHOCTИKИ
 ,ATX,ERROFLG*
 ,XTA,TERCA.
 ,ATX,OUTC.
 R,VJM,FORM NC.ЗA HOMEPOM ПK
 ,XTA,T/OШ.
 ,ATX,OUTC+2.'OШИБKA'
 R,VTM,1-5.
 ,UJ,ERC 1.
 L ERC:,XTA,W/BLAN.ЗAБИBKA
 ,ATX,OUTC+1.HOMEPA
 ,ATX,OUTC+2.'OШИБKИ'
 ,ATX,*ER/TEX.OШ.TEKCT.ПK
 ,xta,TERCA+1
 ,atx,OUTC
 R,WTC,APROC+4.ЗA ИHTEPПP.
 13,VJM,.OШИБ.KAPTЫ
 14,vtm,OUTC+3
 ,its,14
 ,call,cosytiso
 ERC 1:,BSS,
 R,VZM,ERC 2.ПOCЛEД.ДИAГHOCT.
 14,VTM,1.
 R,VLM,FUL RET.BHИЗ-БЛOKИPOBAHO
 ERC 2:,ATX,STM.KAPTA HYЖHA
 ,UJ,READY.
 SAV CRD:,BSS,.
C......   CПACEHИE KAPTЫ-PEЗYЛЬTATA
 ,ITA,R.
 ,ATX,FL SAV.
 R,VTM,1-25.
 S CRD:R,XTA,OUTC+24.
 R,ATX,UNPC+24.
 R,VLM,S CRD.
 14,UJ,.
 FORM NC:,BSS,.
C......   ДOФOPMИPOBAHИE AБCOЛЮTHOГO
C         HOMEPA П/K B ДИAГHOCTИKY
  ,ITA,NC
 ,CALL,OCTTDEC
 ,ASN,64-16
 ,AOX,S1*4/60.
 ,ATX,OUTC+1
 R,UJ,.
 FUL RET:,BSS,.
C......   BOЗBPAT ИЗ KOДИPOBЩИKA
 ,XTA,I13.
 ,ATI,13.
 ,XTA,SUM.OПPEД.TИПA
 ,AEX,REG RET.BOЗBPATA
 ,UIA,REPLACE.- ПPOCTOЙ
 14,J+M,13.- ПEPEKЛЮЧ.
 REPLACE:,BSS,.
C......   CMEHA ГЛOБAЛЬHЫX PEГИCTPOB
C         ПEPEKOДИPOBЩИKA И OБPATИBШEГOCЯ
 12,VTM,1-3.
 REP:12,UTC,SI3.
 ,XTA,.
 12,ITS,3.
 12,UTC,SI3.
 ,STX,.
 12,ATI,3.
 12,VLM,REP.
 13,UJ,.
C--------
 INITCODE:,ENTRY,.
C******   YCTAHOBKA HAЧ.COCTOЯHИЯ ФЛAГOB
 14,VTM,-LEN1.OБHYЛEHИE
 INI 1:,XTA,.'*CODES'
 14,ATX,FL BIN . bug: must be *CODES+LEN1.
 14,VLM,INI 1....
 14,VTM,-LEN2.OБHYЛEHИE
 INI 2:14,ATX,*ER/COS.'*ERRORS'
 14,VLM,INI 2....
C......   ФЛAГИ(PEЖИMЫ) PAБOTЫ
 ,ATX,FL MID.
 ,ATX,FL SAV.
 ,ATX,FL NAM.
 ,ATX,SI3-1.N ПK = 0
 ,XTA,B1.
 ,ATX,STM.
 ,UJ,BEGCHEC*.
C--------
C......   CПEЦ.TEKCTOBЫE KAPTЫ
 STC:,BSS,.
 ,ISO,6H*BINAR
 ,ISO,6H*END'201'B
 ,ISO,6H*END'201'F
 ESTC:,BSS,.
 ,ISO,6H*BLERR
 CNAM:,ISO,6H*NAME''
 M1*5:,LOG,7777 7777 7777 7400.
C......   KOHCTAHTЫ ПO 'BINARY'
 BINCOD:,LOG,77750.
 TNOB:,LOG,3 77773.
 ,ISO,14HHET П/K '47'*END
 ,CONT,10HBINARY'47'  '12'
 ,BSS,.
C......   ДЛЯ ДИAГHOCTИKИ TEKCT.KAPT
 TERCA:,LOG, 2 77776.
 ,LOG,2 77757.
 T/OШ:,ISO,6HOШИБKA
 APROC:,,.
 ,,.
 :,,.
 ,Z00,ERRUPP*.
 :,,.
 ,Z00,ERRCDC*.
 :,,
 ,Z00,ERRM20*.
 :,,
 ,Z00,ERRYПДK*
C......   PAЗHЫE KOHCTAHTOЧKИ
 REG RET:,ISO,6HRETSWI
 PTAPE:,ISO,6H*PTAPE
 W/BLAN:,ISO,6H
 S1*4/60:,ISO,6H''''''''
 B1:,LOG,1.
C......   BHYTPEHHИE ФЛAГИ
 STM:,BSS,1.
 FL SAV:,BSS,1.
 R SW:,BSS,1.
 FL NAM:,BSS,1.
C......   ЯЧEЙKИ CПACEHИЯ
 I13:,BSS,1.
 :,,.
 ,Z00,C.
 ,BSS,1.
 SI3:,BSS,1.
 SUM:,BSS,1.
 ,END,.
